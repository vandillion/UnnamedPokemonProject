raw `
.set LOCALID_ORION, 1
.set LOCALID_GS, 2
.set LOCALID_FS, 3
.set LOCALID_WS, 4
.set LOCALID_LEON, 5
.set LOCALID_GRUNT, 6
.set LOCALID_HARLEY, 7
`

mapscripts VastWoodsTrail_MapScripts {
    MAP_SCRIPT_ON_RESUME {
        call_if_eq(VAR_TEMP_2, 1, ShowMons)
	}
}

script ShowMons {
        playbgm(MUS_NONE, TRUE)
		showobjectat(LOCALID_GS, MAP_VAST_WOODS_TRAIL)
        showobjectat(LOCALID_FS, MAP_VAST_WOODS_TRAIL)
        showobjectat(LOCALID_WS, MAP_VAST_WOODS_TRAIL)
}

script StartWalkTrigger0 {
	setvar(VAR_TEMP_0, 0)
	goto(StartWalk)
}
script StartWalkTrigger1 {
	setvar(VAR_TEMP_0, 1)
	goto(StartWalk)
}

script StartWalk {
	lock
	setflag(FLAG_HIDE_ORION_ORIONS_ROOM)
	setobjectxy(LOCALID_ORION, 4, 1)
	msgbox("Hey!\n{PLAYER}!")
	closemessage
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    switch (var(VAR_TEMP_0)) {
        case 0:
            applymovement(LOCALID_ORION, OrionWalkFromTown0)
		    waitmovement(0)
		    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
        case 1:
            applymovement(LOCALID_ORION, OrionWalkFromTown1)
		    waitmovement(0)
		    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    }
	msgbox(format("You ready for our walk?"), MSGBOX_AUTOCLOSE)
    switch (var(VAR_TEMP_0)) {
        case 0:
            applymovement(LOCALID_ORION, OrionWalk0)
            applymovement(OBJ_EVENT_ID_PLAYER, PlayerWalk0)
        case 1:
            applymovement(LOCALID_ORION, OrionWalk1)
        applymovement(OBJ_EVENT_ID_PLAYER, PlayerWalk1)
    }
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
    turnobject(LOCALID_ORION, DIR_NORTH)
    msgbox(format("This forest is home to a plethora of Pokemon, {PLAYER}!"), MSGBOX_AUTOCLOSE)
    playbgm(MUS_NONE, FALSE)
    playmoncry(SPECIES_SUGARGLIDE, CRY_MODE_ENCOUNTER)
    delay(20)
    playmoncry(SPECIES_PARMI, CRY_MODE_ENCOUNTER)
    delay(20)
    playmoncry(SPECIES_RADPOLE, CRY_MODE_ENCOUNTER)
    delay(20)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    applymovement(LOCALID_ORION, Common_Movement_ExclamationMarkDelay)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMarkDelay)
    playse(SE_PIN)
    waitmovement(0)
    msgbox("What was that!?")
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("{PLAYER}, we have to go check what those noises were!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_ORION, RunToChickens0)
    applymovement(OBJ_EVENT_ID_PLAYER, RunToChickens1)
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    msgbox("Hold on!\nSomething's coming towards us!", MSGBOX_AUTOCLOSE)
    playmoncry(SPECIES_SUGARGLIDE, CRY_MODE_NORMAL)
    applymovement(LOCALID_GS, RunToPlayer0)
    waitmovement(0)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    turnobject(LOCALID_ORION, DIR_NORTH)
    msgbox("Huh?\nWho's this Pokemon?")
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("There's more coming from the forest!"))
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    playmoncry(SPECIES_PARMI, CRY_MODE_NORMAL)
    applymovement(LOCALID_FS, RunToPlayer1)
    delay(40)
    playmoncry(SPECIES_RADPOLE, CRY_MODE_NORMAL)
    applymovement(LOCALID_WS, RunToPlayer2)
    waitmovement(0)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("I've never seen any of these Pokemon before!\p"
                  "And they don't look native to the Vast Woods.\p"
                  "Where could they have came from?"), MSGBOX_AUTOCLOSE)
    delay(30)
    message("???: You two!\nDon't move!")
    waitmessage
    waitbuttonpress
    playbgm(MUS_HELP, TRUE)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    delay(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMarkDelay)
    applymovement(LOCALID_ORION, Common_Movement_ExclamationMark)
    applymovement(LOCALID_LEON, GoonsWalkTowardsPlayer)
    applymovement(LOCALID_GRUNT, GoonsWalkTowardsPlayer)
    applymovement(LOCALID_HARLEY, GoonsWalkTowardsPlayer)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    closemessage
    waitmovement(LOCALID_LEON)
    delay(30)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("And who might you three be?")
    waitmovement(0)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("That doesn't matter.\nThose Pokemon are ours!\pReturn them to us immediately!")
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
    delay(60)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("Why would we do that when not only were these Pokemon running away from you, but you look like you're dressed for the Winter Olympics?\p"
                  "I'll ask again.\nWho EXACTLY are you?"))
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("Fine!{PAUSE 40}\n"
                  "I am Leon of the brilliant group that is Subzero!\p"
                  "Now return the Pokemon!"))
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("Not without a fight!")
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("{PLAYER}, it looks like we're about to have our first Pokemon battle!"))
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("Attempting to use our own against us in a battle?\p"
                  "Fine!\nWe'll gladly accept your challenge!"))
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("Are you ready, {PLAYER}?\nChoose a Pokemon to battle with!\p"
                  "I'll choose after you!"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    fadescreen(FADE_TO_BLACK)
    special(ChooseStarter)
    waitstate
    playse(SE_BALL_OPEN)
    switch (var(VAR_STARTER_MON)) {
        case 0:
            call(ContinueGrass)
        case 1:
            call(ContinueFire)
        case 2:
            call(ContinueWater)
    }
    clearflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
    clearflag(FLAG_HIDE_POKEMON_WOODRIDGE_TOWN)
    setvar(VAR_NEWGAME_START, 5)
    switch (var(VAR_RESULT)) {
        case 1:
            special(HealPlayerParty)
            goto(wonFirstBattle)
    }
    special(HealPlayerParty)
    goto(lostFirstBattle)
}

movement LookrightExclam {
	walk_in_place_right
	emote_exclamation_mark
	delay_16 * 3
}

movement HarleyWalkLeft {
    emote_exclamation_mark
    delay_16 * 3
    walk_left * 2
}

movement RunBackHome0 {
    walk_fast_left * 8
}

movement PlayerRunHome {
    player_run_left * 8
}

movement RunBackHome1 {
    walk_fast_right
    walk_fast_left * 9
}


movement RunBackHome2 {
    walk_fast_right * 2
    walk_fast_left * 9
}

movement WalkLeftTurn {
    walk_left * 2
    walk_in_place_right
}

script lostFirstBattle {
    setflag(FLAG_LOST_FIRST_BATTLE)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("You lost the battle.\nNow give us the Pokemon!"))
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("Still not happening!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("{PLAYER}, whether we won or not doesn't matter right now.\pLet's just head back home with the Pokemon!"))
    call(PlayerRivalRunBackHome)
    special(RemoveCameraObject)
    warpsilent(MAP_WOODRIDGE_TOWN, 14, 20)
    waitstate
	release
	end
}

script wonFirstBattle {
    playbgm(MUS_HELP, TRUE)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("Alright, {PLAYER}!\nWe won our first battle!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("Fine!\nTake the Pokemon!\pWe didn't need them anyway!")
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("I know you're bluffing, but we'll leave anyway. Bye!"))
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox(format("{PLAYER}, let's head back home with the Pokemon!"))
    call(PlayerRivalRunBackHome)
    applymovement(OBJ_EVENT_ID_CAMERA, MoveCamToGoons)
    delay(1)
    setobjectxy(LOCALID_GS, 4, 20)
    setobjectxy(LOCALID_FS, 5, 20)
    setobjectxy(LOCALID_WS, 6, 20)
    waitmovement(0)
    applymovement(LOCALID_LEON, WalkLeftTurn)
    waitmovement(0)
    message(format("What the hell was that?!\pHarley, we could've beaten them if you helped!"))
    waitmessage
    applymovement(LOCALID_HARLEY, HarleyWalkLeft)
    waitmovement(0)
    msgbox(format("Excuse me!?\nThat battle had absolutely nothing to do with me!\p"
                  "Besides, have you forgotten exactly why we joined this group in the first place?"))
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("To be fair, you brought me into this mess and I started diggin' it.\p"
                  "Besides, you know the consequences are if they find out you're trying to take this group down!"))
    applymovement(LOCALID_HARLEY, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("You are very stubborn.\pI'm done with you.")
    applymovement(LOCALID_LEON, Common_Movement_ExclamationMarkDelay)
    playse(SE_PIN)
    waitse
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("What?!\nWhat are you on about, Harley??")
    applymovement(LOCALID_HARLEY, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("Bye, Leon.", MSGBOX_AUTOCLOSE)
    playse(SE_FLEE)
    applymovement(LOCALID_HARLEY, VastWoodsTrailHarleyWalkOffscreen)
    delay(16)
    turnobject(LOCALID_LEON, DIR_NORTH)
    message(format("Harley!\n{PAUSE 40}Wait!"))
    delay(24)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    waitmovement(LOCALID_HARLEY)
    delay(24)
    message(format("Where the fuck are you even going...{PAUSE 75}"))
    waitmessage
    closemessage
    delay(40)
    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("Dude...{PAUSE 45}\nAre you going to be okay?"), MSGBOX_AUTOCLOSE)
    delay(45)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("Yeah.{PAUSE 45}\nYeah,{PAUSE 45} I will.\pI just wonder what she plans on doing."), MSGBOX_AUTOCLOSE)
    delay(60)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("Let's start heading back to the base.\pThe Boss is already going to be furious for us being out here."))
    applymovement(LOCALID_LEON, VastWoodsTrailLeonWalkToGrunt)
    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    message(format("Agreed, but what are you going to tell her when she arrives?"))
    delay(8)
    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceDown)
    waitmessage
    waitbuttonpress
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    msgbox("The truth.", MSGBOX_AUTOCLOSE)
    delay(16)
    applymovement(LOCALID_LEON, VastWoodsTrailLeonWalkOffscreen)
    applymovement(LOCALID_GRUNT, VastWoodsTrailGruntWalkOffscreen)
    waitmovement(0)
    warpsilent(MAP_WOODRIDGE_TOWN, 14, 20)
    waitstate
	release
	end
}

script PlayerRivalRunBackHome {    
    closemessage
    special(SpawnCameraObject)
    playse(SE_FLEE)
    applymovement(LOCALID_ORION, RunBackHome0)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerRunHome)
    applymovement(LOCALID_GS, RunBackHome1)
    applymovement(LOCALID_FS, RunBackHome1)
    applymovement(LOCALID_WS, RunBackHome2)
    delay(60)
    fadenewbgm(MUS_NONE)
    delay(60)
}

script ContinueAfterStarterSelection {
    delay(8)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox("Harley, help me fight these guys!")
    applymovement(LOCALID_HARLEY, HarleyMoveAway)
    waitmovement(0)
    msgbox("Absolutely not!")
    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox("Fine!\nUs two will take you down!")
    special(SavePlayerParty)
}

script ContinueGrass {
    applymovement(LOCALID_GS, Common_Movement_EnterBall)
    waitse
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(OrionIllTakeThisOne)
    playse(SE_BALL_OPEN)
    applymovement(LOCALID_FS, Common_Movement_EnterBall)
    waitse
    call(ContinueAfterStarterSelection)
    setvar(VAR_TEMP_2, 1)
    multi_fixed_2_vs_2(TRAINER_MAXIE_MOSSDEEP, LeonDefeatText, TRAINER_TABITHA_MOSSDEEP, GruntDefeatText, PARTNER_ORION_FIRE)
}

script ContinueFire {
    applymovement(LOCALID_FS, Common_Movement_EnterBall)
    waitse
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(OrionIllTakeThisOne)
    playse(SE_BALL_OPEN)
    applymovement(LOCALID_WS, Common_Movement_EnterBall)
    waitse
    call(ContinueAfterStarterSelection)
    setvar(VAR_TEMP_2, 1)
    multi_fixed_2_vs_2(TRAINER_MAXIE_MOSSDEEP, LeonDefeatText, TRAINER_TABITHA_MOSSDEEP, GruntDefeatText, PARTNER_ORION_WATER)
}

script ContinueWater {
    applymovement(LOCALID_WS, Common_Movement_EnterBall)
    waitse
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(OrionIllTakeThisOne)
    playse(SE_BALL_OPEN)
    applymovement(LOCALID_GS, Common_Movement_EnterBall)
    waitse
    call(ContinueAfterStarterSelection)
    setvar(VAR_TEMP_2, 1)
    multi_fixed_2_vs_2(TRAINER_MAXIE_MOSSDEEP, LeonDefeatText, TRAINER_TABITHA_MOSSDEEP, GruntDefeatText, PARTNER_ORION_GRASS)
}


text OrionIllTakeThisOne {format("...{PAUSE 50}\nAlright, then I'll use this one!")}

text LeonDefeatText {format("What?! You used our Pokemon against us and won!\pIs that even fair?!")}

text GruntDefeatText {format("Boss ain't gonna be happy to hear this...")}

movement VastWoodsTrailHarleyWalkOffscreen {
    walk_fast_left
    walk_fast_up
    walk_fast_left * 10
}

movement VastWoodsTrailLeonWalkToGrunt {
    walk_right * 2
}

movement VastWoodsTrailLeonWalkOffscreen {
    walk_right * 10
}

movement VastWoodsTrailGruntWalkOffscreen {
    walk_down
    walk_right * 10
}

movement HarleyMoveAway {
    emote_exclamation_mark
    delay_16
    delay_16
    delay_16
    lock_facing_direction
    walk_right}


movement GoonsWalkTowardsPlayer {walk_left * 5}

movement MoveCamToGoons {walk_right * 2}

movement RunToPlayer0 {
    walk_fast_left * 8
    walk_fast_up
    walk_fast_left * 2
    walk_fast_down
    walk_in_place_fast_right
}
movement RunToPlayer1 {
    walk_fast_left * 8
    walk_fast_down
    walk_fast_left * 2
    walk_fast_up
    walk_in_place_fast_right
}

movement RunToPlayer2 {
    walk_fast_left * 8
    walk_fast_up
    walk_fast_left * 3
    walk_fast_down
    walk_in_place_fast_right
}

movement RunToChickens0 {walk_fast_right * 12}

movement RunToChickens1 {player_run_right * 12}

movement OrionWalkFromTown0 {
    set_visible
    walk_down * 6
    walk_in_place_left
}

movement OrionWalkFromTown1 {
    set_visible
    walk_down * 3
    walk_left
    walk_down * 3
    walk_in_place_right
}

movement PlayerWalk0 {
    walk_right
    walk_down * 2
    walk_right
    walk_down
    walk_right
    walk_down
    walk_right * 17
    walk_up
    walk_right
}

movement PlayerWalk1 {
    walk_left
    walk_down * 2
    walk_right
    walk_down
    walk_right
    walk_down
    walk_right * 18
    walk_up
    walk_right
}

movement OrionWalk0 {
    walk_down * 2
    walk_right
    walk_down
    walk_right
    walk_down
    walk_right * 18
}


movement OrionWalk1 {
    walk_down * 2
    walk_right
    walk_down
    walk_right
    walk_down
    walk_right * 19
}