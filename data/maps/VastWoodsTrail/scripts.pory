const LOCALID_ORION = 1
const LOCALID_GS = 2
const LOCALID_FS = 3
const LOCALID_WS = 4
const LOCALID_LEON = 5
const LOCALID_GRUNT = 6
const LOCALID_HARLEY = 7

mapscripts VastWoodsTrail_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_VAST_WOODS_TRAIL_STATE, 0 { // Player's walk with Orion
            lock
            setflag(FLAG_HIDE_ORIONS_ROOM_ORION)
            applymovement(LOCALID_ORION, moves(set_invisible
                                               walk_faster_up * 12
                                               set_visible))
            msgbox("Hey!\n{PLAYER}!")
            closemessage
            waitmovement(0)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
            applymovement(LOCALID_ORION, moves(walk_down* 3))
            getobjectcurrentxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0, VAR_TEMP_1)
            waitmovement(0)
            switch(var(VAR_TEMP_0)){
                case 13:
                    applymovement(LOCALID_ORION, moves(walk_left * 2
                                                       walk_down * 3
                                                       walk_left))
                    delay(64)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
                case 14:
                    applymovement(LOCALID_ORION, moves(walk_left * 2
                                                       walk_down * 3
                                                       walk_in_place_left))
                    delay(64)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
                case 15:
                    applymovement(LOCALID_ORION, moves(walk_left
                                                       walk_down * 3
                                                       walk_in_place_left))
                    delay(48)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
                case 16:
                    applymovement(LOCALID_ORION, moves(walk_down * 3
                                                       walk_in_place_left))
                    delay(32)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
                case 17:
                    applymovement(LOCALID_ORION, moves(walk_down * 2))
                case 18:
                    applymovement(LOCALID_ORION, moves(walk_down * 3
                                                       walk_in_place_right))
                    delay(32)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
                case 19:
                    applymovement(LOCALID_ORION, moves(walk_right
                                                       walk_down * 3
                                                       walk_in_place_right))
                    delay(48)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
                case 20:
                    applymovement(LOCALID_ORION, moves(walk_right * 2
                                                       walk_down * 3
                                                       walk_in_place_right))
                    delay(64)
                    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
            }
            waitmovement(0)
            msgbox(format("You ready for our walk?"), MSGBOX_AUTOCLOSE)
            // movement sequences. Make sure Player ends up at 19 27 and orion at 19 28
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
            turnobject(LOCALID_ORION, DIR_NORTH)
            msgbox(format("This forest is home to a plethora of Pokémon, {PLAYER}!"), MSGBOX_AUTOCLOSE)
            playbgm(MUS_NONE, FALSE)
            playmoncry(SPECIES_JUNGLIDE, CRY_MODE_ENCOUNTER)
            delay(20)
            playmoncry(SPECIES_PARMI, CRY_MODE_ENCOUNTER)
            delay(20)
            playmoncry(SPECIES_RADPOLE, CRY_MODE_ENCOUNTER)
            delay(20)
            turnobject(LOCALID_ORION, DIR_EAST)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
            applymovement(LOCALID_ORION, Common_Movement_ExclamationMark2)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark2)
            playse(SE_PIN)
            waitmovement(0)
            msgbox("What was that!?")
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            msgbox(format("{PLAYER}, we have to go check what those noises were!"), MSGBOX_AUTOCLOSE)
            applymovement(LOCALID_ORION, moves(walk_fast_right * 15))
            applymovement(OBJ_EVENT_ID_PLAYER, moves(player_run_right * 15))
            waitmovement(0)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
            msgbox("Hold on!\nSomething's coming towards us!", MSGBOX_AUTOCLOSE)
            playmoncry(SPECIES_JUNGLIDE, CRY_MODE_NORMAL)
            applymovement(LOCALID_GS, moves(walk_fast_left * 2
                                            walk_fast_down
                                            walk_fast_left * 6
                                            walk_fast_up
                                            walk_fast_left * 2
                                            walk_fast_down
                                            walk_in_place_fast_right))
            waitmovement(0)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox("Huh?\nWho's this Pokémon?")
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("There's more coming from the forest!"))
            closemessage
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
            playmoncry(SPECIES_PARMI, CRY_MODE_NORMAL)
            applymovement(LOCALID_FS, moves(walk_fast_left * 2
                                            walk_fast_down * 2
                                            walk_fast_left * 6
                                            walk_fast_down
                                            walk_fast_left * 2
                                            walk_fast_up
                                            walk_in_place_fast_right))
            delay(40)
            playmoncry(SPECIES_RADPOLE, CRY_MODE_NORMAL)
            applymovement(LOCALID_WS, moves(walk_fast_left * 2
                                            walk_fast_down
                                            walk_fast_left * 6
                                            walk_fast_up
                                            walk_fast_left * 3
                                            walk_fast_down
                                            walk_in_place_fast_right))
            waitmovement(0)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox(format("I've never seen any of these Pokémon before!\p"
                        "And they don't look native to the Vast Woods.\p"
                        "Where could they have came from?"), MSGBOX_AUTOCLOSE)
            delay(90)
            message("???: You two!\nDon't move!{PAUSE 10}")
            waitmessage
            playbgm(MUS_HELP, TRUE)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
            delay(16)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark2)
            applymovement(LOCALID_ORION, Common_Movement_ExclamationMark)
            applymovement(LOCALID_LEON, moves(walk_left
                                              walk_down
                                              walk_left
                                              walk_down
                                              walk_left * 3))
            applymovement(LOCALID_GRUNT, moves(delay_16
                                               walk_left
                                               walk_down
                                               walk_left * 4))
            applymovement(LOCALID_HARLEY, moves(delay_16 * 3
                                                walk_left
                                                walk_down
                                                walk_left
                                                walk_down
                                                walk_left * 2))
            waitmovement(OBJ_EVENT_ID_PLAYER)
            closemessage
            waitmovement(LOCALID_LEON)
            delay(30)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox("And who might you three be?")
            waitmovement(0)
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox("Who we are doesn't matter.\nWhat does is those Pokémon are ours!\pReturn them to us immediately!")
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
            delay(60)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("Why would we do that when not only were these Pokémon running away from you, but you look like you're dressed for the Winter Olympics?\p"
                          "I'll ask again.\nWho EXACTLY are you?"))
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox(format("Fine!{PAUSE 40}\nWe are members of the brilliant group that is Subzero!\p"
                          "Now return the Pokémon!"))
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox("Not so fast!\nWe're not going down without a fight!")
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            msgbox(format("{PLAYER}, are you thinking what I'm thinking?\pLooks like we're about to have our first Pokémon battle!"))
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            message(format("Do you seriously think you'll win a Pokémon battle against us using OUR Pokémon?"))
            delay(16)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmessage
            waitbuttonpress
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("There's no shame in trying!\nPlus, I don't think you heard me the first time.\p"
                          "{PLAYER} and I are not going down without a fight!"))
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox(format("Fine! We accept your challenge!"))
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            msgbox(format("Are you ready, {PLAYER}?\nChoose a Pokémon to battle with!\l"
                          "I'll choose after you!"), MSGBOX_AUTOCLOSE)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            delay(8)
            fadescreen(FADE_TO_BLACK)
            special(ChooseStarter)
            waitstate
            switch (var(VAR_STARTER_MON)) {
                case 0:
                    setvar(VAR_TEMP_2, LOCALID_GS)
                    setvar(VAR_TEMP_3, LOCALID_FS)
                    setvar(VAR_TEMP_4, PARTNER_ORION_FIRE)
                    setseenmon(SPECIES_RADPOLE)
                case 1:
                    setseenmon(SPECIES_JUNGLIDE)
                    setvar(VAR_TEMP_2, LOCALID_FS)
                    setvar(VAR_TEMP_3, LOCALID_WS)
                    setvar(VAR_TEMP_4, PARTNER_ORION_WATER)
                case 2:
                    setvar(VAR_TEMP_3, LOCALID_GS)
                    setvar(VAR_TEMP_4, PARTNER_ORION_GRASS)
                    setseenmon(SPECIES_PARMI)
                    setvar(VAR_TEMP_2, LOCALID_WS)
            }
            playse(SE_BALL_OPEN)
            applymovement(VAR_TEMP_2, Common_Movement_EnterBall)
            waitse
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            message(format("… … …{PAUSE 40}\nAlright, I'll use this one!{PAUSE 8}"))
            waitmessage
            playse(SE_BALL_OPEN)
            applymovement(VAR_TEMP_3, Common_Movement_EnterBall)
            waitse
            delay(8)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox("Harley, help me fight these guys!")
            playse(SE_PIN)
            applymovement(LOCALID_HARLEY, moves(emote_exclamation_mark
                                                delay_16 * 2
                                                lock_facing_direction
                                                walk_right))
            waitmovement(0)
            msgbox("Absolutely not!")
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            message("Fine!{PAUSE 48}\nUs two will take you down!")
            delay(32)
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
            applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
            waitmessage
            waitbuttonpress
            clearflag(FLAG_HIDE_WOODRIDGE_TOWN_ORION)
            clearflag(FLAG_HIDE_WOODRIDGE_TOWN_POKEMON)
            setvar(VAR_WOODRIDGE_TOWN_STATE, 5)
            setvar(VAR_VAST_WOODS_TRAIL_STATE, 1)
            multi_fixed_2_vs_2(TRAINER_MAXIE_MOSSDEEP, format("What?\nIs that even fair?!"),
                               TRAINER_TABITHA_MOSSDEEP, format("Boss ain't gonna be happy to hear this…"),
                               VAR_TEMP_4)
            special(HealPlayerParty)
            switch (var(VAR_RESULT)) {
                case TRUE: //wonFirstBattle
                    settrainerflag(TRAINER_MAXIE_MOSSDEEP)
                    settrainerflag(TRAINER_TABITHA_MOSSDEEP)
                    playbgm(MUS_HELP, FALSE)
                    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
                    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
                    waitmovement(0)
                    msgbox(format("Alright, {PLAYER}!\nWe won our first Pokémon battle!"), MSGBOX_AUTOCLOSE)
                    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
                    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
                    waitmovement(0)
                    msgbox("Fine! Take the Pokémon!\nWe don't need them anyway!")
                    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    msgbox(format("I know you're bluffing, but we'll leave anyway. Bye!"))
                    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
                    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
                    waitmovement(0)
                    msgbox(format("{PLAYER}, let's head back home with the Pokémon!"))
                    call(PlayerRivalRunBackHome)
                    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_right * 2))
                    applymovement(LOCALID_LEON, moves(walk_left * 2
                                                      walk_in_place_right))
                    waitmovement(0)
                    message(format("What the hell was that?!\nHarley, we could've beaten them if you helped!"))
                    waitmessage
                    playse(SE_PIN)
                    applymovement(LOCALID_HARLEY, moves(emote_exclamation_mark
                                                        delay_16 * 2
                                                        walk_left * 2))
                    waitmovement(0)
                    msgbox(format("Excuse me!?\nThat battle had absolutely nothing to do with me!\p"
                                "Besides, have you forgotten exactly why we joined this group in the first place?"))
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    msgbox(format("To be fair, you brought me into this mess and I started diggin' it.\p"
                                "Besides, you know what the consequences are if they found out you're trying to take this group down!"))
                    applymovement(LOCALID_HARLEY, Common_Movement_WalkInPlaceLeft)
                    waitmovement(0)
                    msgbox("You are very stubborn.\nI'm done with you.")
                    exclam(LOCALID_LEON)
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    msgbox("What?!\nWhat are you on about, Harley??")
                    applymovement(LOCALID_HARLEY, Common_Movement_WalkInPlaceLeft)
                    waitmovement(0)
                    msgbox("Bye, Leon.", MSGBOX_AUTOCLOSE)
                    playse(SE_FLEE)
                    applymovement(LOCALID_HARLEY, moves(walk_fast_left
                                                        walk_fast_up
                                                        walk_fast_left * 10))
                    delay(16)
                    turnobject(LOCALID_LEON, DIR_NORTH)
                    message(format("Harley!\n{PAUSE 40}Wait!"))
                    delay(24)
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
                    waitmovement(LOCALID_HARLEY)
                    delay(24)
                    message(format("Where the fuck are you even going…{PAUSE 75}"))
                    waitmessage
                    closemessage
                    delay(40)
                    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
                    waitmovement(0)
                    msgbox(format("Dude…{PAUSE 45}\nDid she just dump you?"), MSGBOX_AUTOCLOSE)
                    delay(45)
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    msgbox(format("Yeah.{PAUSE 45}\nI think she did."))
                    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
                    waitmovement(0)
                    msgbox(format("Are you gonna be okay?"))
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    msgbox(format("I'll be fine.\nI just wonder what exactly she plans on doing now."), MSGBOX_AUTOCLOSE)
                    delay(60)
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceRight)
                    waitmovement(0)
                    msgbox(format("Let's start heading back to the hideout.\pThe Boss is already going to be furious for us being out here."))
                    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceLeft)
                    delay(8)
                    applymovement(LOCALID_LEON, moves(walk_right * 2))
                    waitmovement(LOCALID_GRUNT)
                    msgbox(format("Agreed, but what are you going to tell her when she arrives?"))
                    applymovement(LOCALID_GRUNT, Common_Movement_WalkInPlaceDown)
                    applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceUp)
                    waitmovement(0)
                    msgbox("Leon: The truth.", MSGBOX_AUTOCLOSE)
                    delay(16)
                    applymovement(LOCALID_LEON, moves(walk_right * 4
                                                       walk_up
                                                       walk_right
                                                       walk_up
                                                       walk_right
                                                       walk_up * 5))
                    applymovement(LOCALID_GRUNT, moves(walk_down
                                                       walk_right * 4
                                                       walk_up
                                                       walk_right
                                                       walk_up
                                                       walk_right
                                                       walk_up * 5))
                    waitmovement(0)
                    delay(30)
                    warpsilent(MAP_WOODRIDGE_TOWN, 16, 25)
                    waitstate
                    release
                    end
            } //lostFirstBattle
            setflag(FLAG_LOST_FIRST_BATTLE)
            applymovement(LOCALID_LEON, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox(format("You lost the battle.\nNow give us the Pokémon!"))
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("Still not happening!"), MSGBOX_AUTOCLOSE)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
            waitmovement(0)                                //SPOILER ALERT: It does!               
            msgbox(format("{PLAYER}, whether we won or not doesn't matter right now.\p"
                        "Let's just head back home with the Pokémon!"), MSGBOX_AUTOCLOSE)
            call(PlayerRivalRunBackHome)
            warpsilent(MAP_WOODRIDGE_TOWN, 16, 25)
            waitstate
            release
            end
        }
    ]
    MAP_SCRIPT_ON_RESUME {
        if(var(VAR_VAST_WOODS_TRAIL_STATE) == 1){
            playbgm(MUS_NONE, TRUE)
	        showobjectat(VAR_TEMP_2, MAP_VAST_WOODS_TRAIL)
            showobjectat(VAR_TEMP_3, MAP_VAST_WOODS_TRAIL)
        }
        end
	}
}

script PlayerRivalRunBackHome {    
    closemessage
    special(SpawnCameraObject)
    playse(SE_FLEE)
    applymovement(LOCALID_ORION, moves(walk_fast_left * 8))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(player_run_left * 8))
    applymovement(LOCALID_GS, moves(walk_fast_right
                                    walk_fast_left * 9))
    applymovement(LOCALID_FS, moves(walk_fast_right
                                    walk_fast_left * 9))
    applymovement(LOCALID_WS, moves(walk_fast_right * 2
                                    walk_fast_left * 10))
    delay(60)
    fadenewbgm(MUS_NONE)
    delay(60)
    removeobject(OBJ_EVENT_ID_PLAYER)
    removeobject(LOCALID_ORION)
    setobjectxyperm(LOCALID_GS, 0, 0)
    setobjectxyperm(LOCALID_FS, 0, 0)
    setobjectxyperm(LOCALID_WS, 0, 0)
	setflag(FLAG_HIDE_NPCS_VAST_WOODS_TRAIL)
    addvar(VAR_VAST_WOODS_TRAIL_STATE, 1)
}