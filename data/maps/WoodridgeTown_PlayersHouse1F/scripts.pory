const LOCALID_MOM = 1

//                                         TODO:
// Player receives costume box and running shoes from boxes in their room	NOT STARTED
// Vast Woods (Trail) becomes accessible to player after the Leon encounter	NOT STARTED

mapscripts WoodridgeTown_PlayersHouse1F_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_NEWGAME_START, 0 { // Player walks down the stairs for the first time
			lock
			playse(SE_PIN)
			exclam(LOCALID_MOM)
			msgbox(format("Mom: Oh! {PLAYER}!\n You're awake!"))
			playbgm(MUS_RG_FOLLOW_ME, FALSE)
			closemessage
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2
											 		 walk_down * 2
													 walk_left
													 walk_down
													 walk_in_place_fast_left))
			waitmovement(0)
			msgbox(format("Wow, sleepyhead! Jet Lag must've hit you big time, huh?\p"
				  		  "Just messing with you, haha!\nNow you don't want to make yourself look like a bad neighbor, do you?\p"
						  "Of course you don't.\nSo, why don't you go introduce yourself?"), MSGBOX_AUTOCLOSE)
			fadedefaultbgm
			setvar(VAR_NEWGAME_START, 1)
			setvar(VAR_BLOCK_PROGRESSION_SOUTH, 1)
			setvar(VAR_BLOCK_PROGRESSION_WEST, 1)
			release
			end
		}
		VAR_NEWGAME_START, 3 { // Player has a chat with Mom before heading out for walk with Orion
			lock
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 3
													 walk_up * 2
													 walk_in_place_left))
			waitmovement(0)
			applymovement(LOCALID_MOM, Common_Movement_WalkInPlaceRight)
			waitmovement(0)
			msgbox(format("Did you introduce yourself to the neighbors, {PLAYER}?"), MSGBOX_AUTOCLOSE)
			delay(60)
			msgbox(format("…\n…\p"
						  "The kid next door asked to take you on a walk into the woods?\p"
						  "Well, sounds like you two will have a great time.\p"
						  "Just be careful, please {PLAYER}!\nRemember, we just moved here and we aren't familiar with the environment.\p"
						  "Oh!\nAnd take these, too!"))
			playfanfare(MUS_OBTAIN_ITEM)
			message(format("{PLAYER} received the Running Shoes!"))
			waitfanfare
			setflag(FLAG_SYS_B_DASH)
			setflag(FLAG_RECEIVED_RUNNING_SHOES)
			msgbox(format("If anything happens to you let me know."), MSGBOX_AUTOCLOSE)
			setvar(VAR_NEWGAME_START, 4)
			setvar(VAR_BLOCK_PROGRESSION_SOUTH, 2)
			release
			end
		}
    ]
}

script WoodridgePlayersHouse1F_Mom {
	if(var(VAR_NEWGAME_START) < 4 ) {
		msgbox(format("I saw that there were other people in this town.\p"
					  "Don't let yourself look like a bad neightbor, {PLAYER}. Go introduce yourself!"), MSGBOX_NPC)
	} elif(var(VAR_NEWGAME_START) < 6) {
		msgbox(format("If anything happens to you let me know."), MSGBOX_NPC)
	} elif(var(VAR_TEMP_1) == TRUE) {
		msgbox("You look all refreshed!\nTake care, {PLAYER}!", MSGBOX_NPC)
	} else {
		lock
		faceplayer
		msgbox("Oh! {PLAYER}!\nYou should take a rest.", MSGBOX_AUTOCLOSE)
		playse(SE_USE_ITEM)
		special(HealPlayerParty)
		waitse
		msgbox("There. You look all refreshed!\nTake care!", MSGBOX_NPC)
		setvar(VAR_TEMP_1, TRUE)
		incrementgamestat(GAME_STAT_RESTED_AT_HOME)
		release
	}
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	end
}