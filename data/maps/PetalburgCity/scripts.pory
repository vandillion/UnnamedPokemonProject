mapscripts PetalburgCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_ANIMORA_CITY)
		setrespawn(HEAL_LOCATION_PETALBURG_CITY)
		end
	}
}

// =========================================================================================================================================
raw `
.set LOCALID_GIRL, 1
`

script AnimoraCityHoodedGirlBattle {
	lock
	switch (var(VAR_TEMP_0)) {
        case 0:
			setobjectxy(LOCALID_GIRL, 39, 6)
        case 1:
			setobjectxy(LOCALID_GIRL, 39, 7)
    }
	playbgm(MUS_NONE, TRUE)
	msgbox("???: YOU!", MSGBOX_AUTOCLOSE)
	playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
	delay(16)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	playse(SE_PIN)
	delay(8)
	applymovement(LOCALID_GIRL, moves(walk_fast_left * 7))
	waitmovement(0)
	delay(30)
	msgbox("Battle me.\nNow!")
	if (!flag(FLAG_LOST_FIRST_BATTLE)) {
		switch (var(VAR_STARTER_MON)) {
        	case 0:
				trainerbattle_no_intro_no_whiteout(TRAINER_MAY_ROUTE_103_TREECKO, HoodedGirlDefeat)
        	case 1:
				trainerbattle_no_intro_no_whiteout(TRAINER_MAY_ROUTE_103_TORCHIC, HoodedGirlDefeat)
        	case 2:
				trainerbattle_no_intro_no_whiteout(TRAINER_MAY_ROUTE_103_MUDKIP, HoodedGirlDefeat)
	    }
    } else {
		switch (var(VAR_STARTER_MON)) {
        	case 0:
				trainerbattle_no_intro_no_whiteout(TRAINER_MAY_ROUTE_110_TREECKO, HoodedGirlDefeat)
        	case 1:
				trainerbattle_no_intro_no_whiteout(TRAINER_MAY_ROUTE_110_TORCHIC, HoodedGirlDefeat)
        	case 2:
				trainerbattle_no_intro_no_whiteout(TRAINER_MAY_ROUTE_110_MUDKIP, HoodedGirlDefeat)
    	}
	}
	msgbox("… … …\pWe'll meet again…")
	closemessage
	playse(SE_FLEE)
	switch (var(VAR_TEMP_0)) {
        case 0:
			applymovement(LOCALID_GIRL, moves(walk_fast_down
											  walk_fast_left * 2
											  walk_diag_southwest
											  walk_fast_left
											  walk_diag_southwest
											  walk_fast_left * 4))
			delay(8)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
        case 1:
			applymovement(LOCALID_GIRL, moves(walk_fast_up
											  walk_fast_left * 2
											  walk_diag_southwest
											  walk_fast_left
											  walk_diag_southwest
											  walk_fast_left * 4))
			delay(8)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    }
	delay(16)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
	waitmovement(0)
	fadedefaultbgm
	removeobject(LOCALID_GIRL)
	special(HealPlayerParty)
	setflag(FLAG_HIDE_GIRL_ANIMORA_CITY)
	setvar(VAR_ANIMORA_CITY_STATE, 2)
	delay(16)
	release
	end
	}

text HoodedGirlDefeat {
	"… … …\n… … …"
}

script AnimoraCityHoodedGirlBattleTrigger0 {
	setvar(VAR_TEMP_0, 0)
	goto(AnimoraCityHoodedGirlBattle)
}

script AnimoraCityHoodedGirlBattleTrigger1 {
	setvar(VAR_TEMP_0, 1)
	goto(AnimoraCityHoodedGirlBattle)
}