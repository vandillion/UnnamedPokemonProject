const LOCALID_OLD_WOMAN = 1

mapscripts PetalburgCity_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_ANIMORA_CITY)
		setrespawn(HEAL_LOCATION_PETALBURG_CITY)
		if(!var(VAR_ANIMORA_CITY_STATE)) {
			setobjectxyperm(LOCALID_OLD_WOMAN, 32, 8)
			setobjectmovementtype(LOCALID_OLD_WOMAN, MOVEMENT_TYPE_LOOK_AROUND)
		}
		end
	}
}

// Signs

script AnimoraCitySign {
	msgbox("Animora City", MSGBOX_SIGN)
	end
}

script BoutiqueSign {
	msgbox("Closed for renovations.\nSorry in advance!\p-Boutique Mgnt", MSGBOX_SIGN)
	end
}

// NPCs

script AnimoraOldLady {
	if(!var(VAR_ANIMORA_CITY_STATE))
	{
		if(!var(VAR_TEMP_0))
		{
			msgbox(format("You seem to be a new face.\nHave you stopped by Professor Watson's Lab yet?\p"
			              "Fufufu… I may be old, but my bones can still sense a rookie Pokémon Trainer!\p"
						  "Professor Watson's Lab is the western-most building.\l"
						  "Why not pay him a visit, young one?"), MSGBOX_NPC)
			setvar(VAR_TEMP_0, 1)
		}
		else
		{
			msgbox(format("Professor Watson's Lab is the western-most building.\l"
						  "Why not pay him a visit, young one?"), MSGBOX_NPC)
		}
	}
	else
	{
		msgbox(format("I grew old with Animora.\nSeveral years have passed, however, and there's still not much to say about this small city."), MSGBOX_NPC)
	}
	end
}

//Triggers

script AnimoraCityBlockPlayerFromLeaving {
	lock
	turnobject(LOCALID_OLD_WOMAN, DIR_NORTH)
	exclam(LOCALID_OLD_WOMAN)
	message("Hello!\nYou there!")
	delay(8)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	waitmessage
	waitbuttonpress
	closemessage
	getobjectcurrentxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0, VAR_TEMP_1)
	switch(var(VAR_TEMP_1))
	{
		case 6:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right
													 walk_down))
		case 7:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right
													 face_down))
	}
	waitmovement(0)
	msgbox(format("You seem to be a new face.\nHave you stopped by Professor Watson's Lab yet?\p"
			      "Fufufu… I may be old, but my bones can still sense a rookie Pokémon Trainer!\p"
				  "Professor Watson's Lab is the western-most building.\l"
				  "Why not pay him a visit, young one?"), MSGBOX_AUTOCLOSE)
	applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
	waitmovement(0)
	setvar(VAR_TEMP_0, 1)
	release
	end
}