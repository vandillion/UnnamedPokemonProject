
mapscripts Route102_MapScripts {
}

// Signs

script Route2Sign {
	msgbox("Route 2\n{UP_ARROW} Centra City\l{DOWN_ARROW} Animora City", MSGBOX_SIGN)
	end
}

script UnnamedCaveSignWest {
	msgbox("???????????? Cave\n"
		   "West Entrance", MSGBOX_SIGN)
	end
}

// NPCs

script Route2Boy {
	msgbox(format("I was walking along this route the other day, and a rodent-like Pokemon took my Pokemon's Berry!\pAnd I never got it back…"), MSGBOX_NPC)
	end	
}

// Trainers

script Route2Jimmy {
	trainerbattle_single(TRAINER_CALVIN_1, "You plus me equals a battle!\nLet's go!", "Dang it.")
	msgbox(format("I love Pokemon battles, but I always lose…"), MSGBOX_NPC)
	end
}

script Route2Liv {
	trainerbattle_single(TRAINER_TIANA, format("I want to be just as strong as Leader Aria!"), "My wings will spread another day.")
	msgbox(format("Speaking of, I could go for some wings right now."), MSGBOX_NPC)
	end
}

script Route2Fred {
	trainerbattle_single(TRAINER_RICK, "I caught my Pokemon in the Vast\nWoods.\pCheck em out!", "Oh no!\nMy Bug Pokemon!")
	msgbox(format("Oh well. I haven't had the time to train them yet, anyway."), MSGBOX_NPC)
	end
}

script Route2Allen {
	trainerbattle_single(TRAINER_ALLEN, format("Ha! My Pokemon stole a random kid's Pokemon's Oran Berry!"), "Aww.")
	msgbox(format("There was a kid that walked past here a couple minutes ago.\pHe was pretty strong!\nHave you met him yet?"), MSGBOX_NPC)
	end
}

script Route2Luca {
	trainerbattle_single(TRAINER_JERRY_1, format("You can't possibly beat me in a battle!\pI passed all of my exams at the Trainer's School!"), "Cheater!")
	msgbox(format("Don't make me get my buddy Kyle! He's dealt with Herobrine before!"), MSGBOX_NPC)
	end
}

// Triggers

const LOCALID_MAN = 12

script Route2CatchTutorial {
	lock
	turnobject(LOCALID_MAN, DIR_SOUTH)
	exclam(LOCALID_MAN)
	getobjectcurrentxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0, VAR_RESULT)
	switch(var(VAR_TEMP_0))
	{
		case 19:
			applymovement(LOCALID_MAN, moves(walk_left * 2))
		case 20:
			applymovement(LOCALID_MAN, moves(walk_left))
		case 22:
			applymovement(LOCALID_MAN, moves(walk_right))
		case 23:
			applymovement(LOCALID_MAN, moves(walk_right * 2))
		case 24:
			applymovement(LOCALID_MAN, moves(walk_right * 3))
	}
	waitmovement(0)
	applymovement(LOCALID_MAN, moves(walk_down * 2))
	waitmovement(0)
	msgbox(format("Hello there!\nYou're an up-and-coming Pokémon Trainer, correct?\p"
				  "If you want, I can show you the basics on catching a Pokémon."), MSGBOX_YESNO)
	switch(var(VAR_RESULT))
	{
		case 1:
			msgbox("Alright, follow me!", MSGBOX_AUTOCLOSE)
			switch(var(VAR_TEMP_0))
			{
				case 19:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 4
															 walk_right * 4
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_up * 3
													 walk_right * 4
													 walk_up * 2
													 walk_in_place_fast_down))
				case 20:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 4
															 walk_right * 3
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_up * 3
													 walk_right * 3
													 walk_up * 2
													 walk_in_place_fast_down))
				case 21:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 4
															 walk_right * 2
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_up * 3
													 walk_right * 2
													 walk_up * 2
													 walk_in_place_fast_down))
				case 22:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 4
															 walk_right
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_up * 3
													 walk_right
													 walk_up * 2
													 walk_in_place_fast_down))
				case 23:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 5))
					applymovement(LOCALID_MAN, moves(walk_up * 5
													 walk_in_place_fast_down))
				case 24:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 4
															 walk_left
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_up * 3
													 walk_left
													 walk_up * 2
													 walk_in_place_fast_down))
			}		
			waitmovement(0)
			msgbox(format("You can find wild Pokémon in patches of tall grass, such as this one."))
			applymovement(LOCALID_MAN, moves(walk_in_place_fast_up))
			waitmovement(0)
			msgbox("Oh!\nHere comes one now!")
			closemessage
			special(SavePlayerParty)
			special(LoadWallyZigzagoon)
			special(StartWallyTutorialBattle)
			waitstate
			special(LoadPlayerParty)
			applymovement(LOCALID_MAN, moves(walk_in_place_fast_down))
			waitmovement(0)
			if (!checkitem(ITEM_POKE_BALL)) {
				msgbox("And that's how catching a wild Pokémon\nis done!\pWait! Before I leave, take these!\nI have plenty of spares.")
				giveitem(ITEM_POKE_BALL, 10)
				call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
				msgbox(format("These are tools used to catch wild Pokémon.\pRemember to weaken the wild Pokémon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			} else {
				msgbox(format("And that's how catching a wild Pokémon is done!\pAlways remember to weaken the wild Pokémon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			}
			applymovement(LOCALID_MAN, moves(walk_left * 4
											 walk_down
											 walk_left * 6))
		case 0:
			if (!checkitem(ITEM_POKE_BALL)) {
				msgbox(format("Oh, okay.\nI still want you to have these, anyway!"))
				giveitem(ITEM_POKE_BALL, 10)
				call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
				msgbox(format("These are tools used to catch wild Pokémon.\pJust remember to weaken the wild Pokémon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			} else {
				msgbox(format("Oh, okay.\nJust remember to weaken the wild Pokémon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			}
			waitmovement(0)
			applymovement(LOCALID_MAN, moves(walk_up * 3
											 walk_left * 10))
	}
	waitmovement(0)
	removeobject(LOCALID_MAN)
	setflag(FLAG_HIDE_ROUTE_2_CATCH_TUTORIAL)
	setvar(VAR_ROUTE2_STATE, 1)
	release
	end
}