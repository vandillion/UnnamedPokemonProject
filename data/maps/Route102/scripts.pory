
mapscripts Route102_MapScripts {
}

// Signs

script Route2Sign {
	msgbox("Route 2\n{UP_ARROW} Centra City\l{DOWN_ARROW} Animora City", MSGBOX_SIGN)
	end
}

script UnnamedCaveSignWest {
	msgbox("???????????? Cave\n"
		   "West Entrance", MSGBOX_SIGN)
	end
}

// NPCs

script Route2Boy {
	msgbox(format("I was walking along this route the other day, and a rodent-like Pokemon took my Pokemon's Berry!\pAnd I never got it back…"), MSGBOX_NPC)
	end	
}

// Trainers

script Route2Jimmy {
	trainerbattle_single(TRAINER_CALVIN_1, format("You plus me equals a battle! Let's go!"), "Dang it.")
	msgbox(format("I love Pokemon battles, but I always lose…"), MSGBOX_NPC)
	end
}

script Route2Liv {
	trainerbattle_single(TRAINER_TIANA, format("I want to be just as strong as Leader Izzie!"), "My wings will spread another day.")
	msgbox(format("Speaking of, I could go for some wings right now."), MSGBOX_NPC)
	end
}

script Route2Fred {
	trainerbattle_single(TRAINER_RICK, "I caught my Pokemon in the Vast\nForest.\pCheck em out!", "Oh no!\nMy Bug Pokemon!")
	msgbox(format("Oh well. I haven't had the time to train them yet, anyway."), MSGBOX_NPC)
	end
}

script Route2Shawn {
	trainerbattle_single(TRAINER_ALLEN, format("Ha! My Pokemon stole a random kid's Pokemon's Oran Berry!"), "Aww.")
	msgbox(format("There was a kid that walked past here a couple minutes ago.\pHe was pretty strong!\nHave you met him yet?"), MSGBOX_NPC)
	end
}

script Route2Luca {
	trainerbattle_single(TRAINER_JERRY_1, format("You can't possibly beat me in a battle!\pI passed all of my exams at the Pokemon Academy!"), "Cheater!")
	msgbox(format("Don't make me get my buddy Kyle! He's dealt with Herobrine before!"), MSGBOX_NPC)
	end
}

// ========================================================================================

const LOCALID_MAN = 12

script Route2CatchTutorial {
	lock
	applymovement(LOCALID_MAN, moves(face_player
									 emote_exclamation_mark
									 delay_16 * 3))
	playse(SE_PIN)
	waitmovement(0)
	switch(var(VAR_TEMP_0))
	{
		case 0:
			applymovement(LOCALID_MAN, moves(walk_up
											 walk_left
											 walk_up
											 walk_left))
		case 1:
			applymovement(LOCALID_MAN, moves(walk_up
											 walk_left * 2))
		case 2:
			applymovement(LOCALID_MAN, moves(walk_left * 2))
		case 3:
			applymovement(LOCALID_MAN, moves(walk_down
                                             walk_left * 2))
		case 4:
			applymovement(LOCALID_MAN, moves(walk_down
											 walk_left
											 walk_down
											 walk_left))
		case 5:
			applymovement(LOCALID_MAN, moves(walk_left
											 walk_down * 3
											 walk_left))
	}
	waitmovement(0)
	msgbox(format("Hello there!\nYou're a Pokemon Trainer, correct?\p"
				  "If you want, I can show you the basics on catching a Pokemon."), MSGBOX_YESNO)
	switch(var(VAR_RESULT))
	{
		case 1:
			msgbox("Alright, follow me!", MSGBOX_AUTOCLOSE)
			switch(var(VAR_TEMP_0))
   			{
				case 0:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 4))
					applymovement(LOCALID_MAN, moves(walk_right * 4))
					waitmovement(0)
					applymovement(LOCALID_MAN, Common_Movement_WalkInPlaceLeft)
				case 1:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 5
															 walk_in_place_up))
					applymovement(LOCALID_MAN, moves(walk_right * 4
													 walk_up
													 walk_in_place_down))
				case 2:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3
					                                         walk_up
															 walk_right * 2
															 walk_in_place_up))
					applymovement(LOCALID_MAN, moves(walk_right * 2
													 walk_up
													 walk_right * 2
													 walk_up
													 walk_in_place_down))
				case 3:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3
															 walk_up * 2
															 walk_right * 2
															 walk_in_place_up))
					applymovement(LOCALID_MAN, moves(walk_right * 2
													 walk_up * 2
													 walk_right * 2
													 walk_up
													 walk_in_place_down))
				case 4:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3
															 walk_up * 2
															 walk_right * 2
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_right * 2
													 walk_up * 2
													 walk_right * 2
													 walk_up * 2
													 walk_in_place_down))
				case 5:
					applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3
															 walk_up * 3
															 walk_right * 2
															 walk_up))
					applymovement(LOCALID_MAN, moves(walk_right * 2
													 walk_up * 3
													 walk_right * 2
													 walk_up * 2
													 walk_in_place_down))
			}
			waitmovement(0)
			msgbox(format("You can find wild Pokemon in patches of tall grass, such as this one."))
			applymovement(LOCALID_MAN, Common_Movement_FaceAwayPlayer)
			waitmovement(0)
			applymovement(LOCALID_MAN, Common_Movement_ExclamationMarkDelay)
			playse(SE_PIN)
			waitmovement(0)
			msgbox("Oh!\nHere comes one now!")
			special(SavePlayerParty)
			special(LoadWallyZigzagoon)
			special(StartWallyTutorialBattle)
			waitstate
			special(LoadPlayerParty)
			applymovement(LOCALID_MAN, Common_Movement_FacePlayer)
			waitmovement(0)
			if (!checkitem(ITEM_POKE_BALL)) {
				msgbox("Take these, too!\nI have plenty of spares.")
				giveitem(ITEM_POKE_BALL, 5)
				call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
				msgbox(format("These are tools used to catch wild Pokemon.\pRemember to weaken the wild Pokemon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			} else {
				msgbox(format("Always remember to weaken the wild Pokemon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			}
			applymovement(LOCALID_MAN, moves(walk_up * 6))
		case 0:
			if (!checkitem(ITEM_POKE_BALL)) {
				msgbox(format("Oh, okay.\nI still want you to have these, anyway!"))
				giveitem(ITEM_POKE_BALL, 5)
				call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
				msgbox(format("These are tools used to catch wild Pokemon.\pJust remember to weaken the wild Pokemon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			} else {
				msgbox(format("Oh, okay.\nJust remember to weaken the wild Pokemon before throwing a Ball."), MSGBOX_AUTOCLOSE)
			}
			applymovement(LOCALID_MAN, moves(walk_right * 4
											 walk_up * 6))
	}
	waitmovement(0)
	removeobject(LOCALID_MAN)
	setflag(FLAG_HIDE_ROUTE_2_CATCH_TUTORIAL)
	setvar(VAR_ROUTE2_STATE, 1)
	release
	end
}

script Route2CatchTutorialTrigger0 {
	setvar(VAR_TEMP_0, 0)
	goto(Route2CatchTutorial)
}

script Route2CatchTutorialTrigger1 {
	setvar(VAR_TEMP_0, 1)
	goto(Route2CatchTutorial)
}

script Route2CatchTutorialTrigger2 {
	setvar(VAR_TEMP_0, 2)
	goto(Route2CatchTutorial)
}

script Route2CatchTutorialTrigger3 {
	setvar(VAR_TEMP_0, 3)
	goto(Route2CatchTutorial)
}

script Route2CatchTutorialTrigger4 {
	setvar(VAR_TEMP_0, 4)
	goto(Route2CatchTutorial)
}

script Route2CatchTutorialTrigger5 {
	setvar(VAR_TEMP_0, 5)
	goto(Route2CatchTutorial)
}