const LOCALID_ORIONS_MOM = 1
const LOCALID_ORION = 2
const LOCALID_PLAYERS_MOM = 3
const LOCALID_GS = 4
const LOCALID_FS = 5
const LOCALID_WS = 6
 
// TODO: Move player's and orion's battle to frame table
mapscripts WoodridgeTown_OrionsHouse1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if(var(VAR_WOODRIDGE_TOWN_STATE) == 1) {
	        setobjectxyperm(LOCALID_ORIONS_MOM, 4, 5)
            setobjectmovementtype(LOCALID_ORIONS_MOM, MOVEMENT_TYPE_FACE_DOWN)
        } elif(var(VAR_WOODRIDGE_TOWN_STATE) == 5) {
            switch (var(VAR_STARTER_MON)) {
                case 0:
			        setobjectmovementtype(LOCALID_GS, MOVEMENT_TYPE_INVISIBLE)
                    setobjectxyperm(LOCALID_FS, 5, 2)
                    setobjectxyperm(LOCALID_WS, 4, 2)
                case 1:
                    setobjectxyperm(LOCALID_GS, 5, 2)
                    setobjectxyperm(LOCALID_FS, 4, 2)
			        setobjectmovementtype(LOCALID_WS, MOVEMENT_TYPE_INVISIBLE)
                case 2:
                    setobjectxyperm(LOCALID_GS, 4, 2)
			        setobjectmovementtype(LOCALID_FS, MOVEMENT_TYPE_INVISIBLE)
			        setobjectxyperm(LOCALID_WS, 5, 2)
            }
        }
		end
	}
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_WOODRIDGE_TOWN_STATE, 5 {
            playbgm(MUS_NONE, TRUE)
	        hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_WOODRIDGE_TOWN_ORIONS_HOUSE1F)
        }
    ]
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_WOODRIDGE_TOWN_STATE, 1 { // Player introduces themselves to Orion's mom
            lock
            exclam(LOCALID_ORIONS_MOM)
            applymovement(LOCALID_ORIONS_MOM, moves(walk_left * 2
                                                    walk_down * 2))
            waitmovement(0)
            msgbox(format("We weren't expecting guests today.\nWho are you?\p"
                          "… … … … …\n… … … … …\p"
                          "So you're our new neighbor?\nNice to meet you, {PLAYER}.\p"
                          "If you're looking for someone to talk to, I have a son who's about your age.\p"
                          "I'm sure he'd like to meet you, but he's not here right now.\p"
                          "He's on one of his nature strolls in the woods south of here."), MSGBOX_AUTOCLOSE)
            applymovement(LOCALID_ORIONS_MOM, moves(walk_up * 2
                                                    walk_right * 6
                                                    walk_down
                                                    walk_in_place_left))
            waitmovement(0)
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            clearflag(FLAG_HIDE_WOODRIDGE_TOWN_ORION)
            setvar(VAR_WOODRIDGE_TOWN_STATE, 2)
            release
            end
        }
		VAR_WOODRIDGE_TOWN_STATE, 5 { // Cutscene in Orion's house after the Vast Woods Trail battle, dubbed "Town Meeting"
            lock
            initclock(9, 0)
	        special(SpawnCameraObject)
	        setobjectxy(OBJ_EVENT_ID_PLAYER, 2, 8)
            delay(45)
            playse(SE_DOOR)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
            applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
                                                     delay_16
                                                     walk_in_place_right))
            waitse
            applymovement(LOCALID_PLAYERS_MOM, moves(walk_in_place_left
                                                     emote_exclamation_mark
                                                     delay_16 * 2))
            applymovement(LOCALID_ORION, moves(walk_in_place_left
                                               emote_exclamation_mark))
            applymovement(LOCALID_ORIONS_MOM, moves(walk_in_place_left
                                                    emote_exclamation_mark))
            delay(16)
            playse(SE_PIN)
            waitmovement(LOCALID_PLAYERS_MOM)
            msgbox("Mom: {PLAYER}!\nAbout time you woke up!", MSGBOX_AUTOCLOSE)
            applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 3
                                                     walk_right * 4))
            delay(84)
            turnobject(LOCALID_PLAYERS_MOM, DIR_NORTH)
            delay(16)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
            waitmovement(OBJ_EVENT_ID_PLAYER)
            special(RemoveCameraObject)
            msgbox(format("Sorry, {PLAYER}! You were still asleep by daybreak, so I told your mom to meet at my house myself."))
            applymovement(LOCALID_PLAYERS_MOM, Common_Movement_WalkInPlaceUp)
            waitmovement(0)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
            turnobject(LOCALID_ORION, DIR_SOUTH)
            msgbox(format("Your friend already told us about what happened last night.\pI'm glad both of you are okay."))
            applymovement(LOCALID_ORIONS_MOM, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            turnobject(LOCALID_PLAYERS_MOM, DIR_EAST)
            turnobject(LOCALID_ORION, DIR_EAST)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
            msgbox(format("As for the shady kids he told us about, we can't really make a decision on them since we don't know if they were intentionally causing trouble.\p"
                        "But, if you happen to get any more information on them, let both us and authorities know."))
            applymovement(LOCALID_PLAYERS_MOM, Common_Movement_WalkInPlaceUp)
            waitmovement(0)
            turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
            turnobject(LOCALID_ORION, DIR_SOUTH)
            msgbox(format("Which leads to my next point.\p{PLAYER} and Orion, since this is your first time “owning” a Pokémon, we want you to pay a visit to the Professor's Lab in Animora City.\p"
                        "What happens next is entirely up to him."
                        "You can take Route 1, which is directly west of here, to get to Animora City."))
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceDown)
            waitmovement(0)
            msgbox("Will do, ma'am!")
            applymovement(LOCALID_PLAYERS_MOM, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("Mom: Alright, I'm gonna get going.\pThank you for having me over, Ms. Fields."))
            applymovement(LOCALID_ORIONS_MOM, Common_Movement_WalkInPlaceLeft)
            waitmovement(0)
            msgbox("Anytime!", MSGBOX_AUTOCLOSE)
            delay(20)
            applymovement(LOCALID_PLAYERS_MOM, moves(walk_down * 2
                                                     walk_left * 3
                                                     walk_in_place_down
                                                     set_invisible))
            delay(96)
            playse(SE_EXIT)
            waitse
            playbgm(MUS_LITTLEROOT, TRUE)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("{PLAYER}, we have a problem!\nFollow me!"), MSGBOX_AUTOCLOSE)
            applymovement(OBJ_EVENT_ID_PLAYER,  moves(walk_right * 3
                                                      walk_up * 2
                                                      walk_left * 4))
            applymovement(LOCALID_ORION, moves(walk_right * 2
                                               walk_up * 2
                                               walk_left * 5
                                               walk_in_place_right))
            waitmovement(0)
            msgbox(format("When I went to go check on the Pokemon this morning, one of them was missing!\p"
                        "I looked all around the Town, but I couldn't find anything…"))
            closemessage
            delay(35)
            msgbox(format("Oh yeah! {PLAYER}, I want you to have this.\pYou can give it to your Pokémon!"))
            giveitem(ITEM_ORAN_BERRY)
            call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("k see ya later"), MSGBOX_AUTOCLOSE)
            applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
            applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
            waitmovement(0)
            switch (var(VAR_STARTER_MON)) {
                case 0:
                    removeobject(LOCALID_WS)
                    applymovement(LOCALID_FS, Common_Movement_EnterBall)
                case 1:
                    removeobject(LOCALID_FS)
                    applymovement(LOCALID_GS, Common_Movement_EnterBall)
                case 2:
                    removeobject(LOCALID_GS)
                    applymovement(LOCALID_WS, Common_Movement_EnterBall)
            }
            delay(20)
            applymovement(LOCALID_ORION, moves(walk_left * 2
                                               walk_up
                                               set_invisible))
            waitmovement(0)
            playse(SE_EXIT)
            waitse
            removeobject(LOCALID_ORION)
            removeobject(LOCALID_PLAYERS_MOM)
            removeobject(LOCALID_GS)
            removeobject(LOCALID_FS)
            removeobject(LOCALID_WS)
            setflag(FLAG_SYS_POKEMON_GET)
            setflag(FLAG_HIDE_ORIONS_HOUSE_CUTSCENE_NPCS)
            setvar(VAR_WOODRIDGE_TOWN_STATE, 6)
            setvar(VAR_BLOCK_PROGRESSION_SOUTH, 1)
            setvar(VAR_BLOCK_PROGRESSION_WEST, 0)
            clearflag(FLAG_HIDE_WOODRIDGE_TOWN_ORION)
            clearflag(FLAG_HIDE_ORIONS_ROOM_ORION)
            release
            end 
        }
    ]
}

// NPCs

script WoodridgeOrionHouse1F_OrionsMom {
    if(var(VAR_WOODRIDGE_TOWN_STATE) < 3) {
        msgbox(format("If you're looking for someone to talk to, I have a son who's about your age.\p"
                      "I'm sure he'd like to meet you, but he's not here right now.\pHe's on one of his nature strolls in the woods south of here."), MSGBOX_NPC)
    } elif(var(VAR_WOODRIDGE_TOWN_STATE) < 6) {
        msgbox("Hello, {PLAYER}.", MSGBOX_NPC)
    } else {
        msgbox(format("Orion was always the adventurous sort."
                      "And he seems to be rather interested in you, {PLAYER}.\p"
                      "Please watch over him for me."), MSGBOX_NPC)
    }
    end
}