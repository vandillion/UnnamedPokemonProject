raw `
.set LOCALID_ORIONS_MOM, 1
.set LOCALID_ORION, 3
.set LOCALID_DAISY, 4
.set LOCALID_SAM, 5
.set LOCALID_GS, 6
.set LOCALID_FS, 7
.set LOCALID_WS, 8
.set LOCALID_ORIONS_STEPDAD, 2
`
mapscripts WoodridgeTown_OrionsHouse1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
		call_if_eq(VAR_NEWGAME_START, 1, MoveMom)
        call_if_eq(VAR_NEWGAME_START, 5, MoveMon)
		end
	}
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		 VAR_NEWGAME_START, 5 : OrionsHouseHidePlayer
    ]
	MAP_SCRIPT_ON_FRAME_TABLE [
		 VAR_NEWGAME_START, 1 : OrionMomIntro
		 VAR_NEWGAME_START, 5 : TownMeeting
    ]
}

script OrionsHouseHidePlayer {
	hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_WOODRIDGE_TOWN_ORIONS_HOUSE1F)
}

script MoveMon {
	switch (var(VAR_STARTER_MON)) {
        case 0:
            setobjectxyperm(LOCALID_GS, 5, 2)
            setobjectxyperm(LOCALID_FS, 4, 2)
			setobjectmovementtype(LOCALID_WS, MOVEMENT_TYPE_INVISIBLE)
        case 1:
            setobjectxyperm(LOCALID_FS, 5, 2)
            setobjectxyperm(LOCALID_WS, 4, 2)
			setobjectmovementtype(LOCALID_GS, MOVEMENT_TYPE_INVISIBLE)
        case 2:
			setobjectxyperm(LOCALID_WS, 5, 2)
            setobjectxyperm(LOCALID_GS, 4, 2)
			setobjectmovementtype(LOCALID_FS, MOVEMENT_TYPE_INVISIBLE)
    }
}

script MoveMom {
	setobjectxyperm(1, 4, 5)
    setobjectmovementtype(1, MOVEMENT_TYPE_FACE_DOWN)
}


script OrionMomIntro {
    lock
    turnobject(1, DIR_SOUTH)
    playse(SE_PIN)
    applymovement(1, Common_Movement_ExclamationMarkDelay)
	waitmovement(0)
    applymovement(1, OrionMomWalkToDoor)
    waitmovement(0)
    msgbox(format("We weren't expecting guests today.\nWho are you?"))
    closemessage
    delay(45)
    msgbox(format("...\pSo you're our new neighbor?\nNice to meet you, {PLAYER}.\pIf you're looking for someone to talk to, we have a son who's about your age.\p"
                  "I'm sure he'd like to meet you, but he's not here right now.\pHe's on one of his nature strolls in the woods south of here."))
    closemessage
    setvar(VAR_NEWGAME_START, 2)
    applymovement(1, MoveMomToTable)
    waitmovement(0)
    clearflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
    release
}

script TownMeeting {
    lock
	special(SpawnCameraObject)
	setobjectxy(OBJ_EVENT_ID_PLAYER, 2, 8)
    message("The next morning...")
    waitmessage
    delay(45)
    closemessage
    delay(45)
    playse(SE_DOOR)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerAppearAtDoor)
    waitse
    applymovement(LOCALID_DAISY, TownMeetingNoticePlayer)
    applymovement(LOCALID_SAM, TownMeetingNoticePlayer)
    applymovement(LOCALID_ORION, TownMeetingNoticePlayer)
    applymovement(LOCALID_ORIONS_MOM, TownMeetingNoticePlayer)
    delay(24)
    playse(SE_PIN)
    waitmovement(0)
    msgbox(format("Mom: {PLAYER}!\nAbout time you woke up!"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, MeetingPlayerWalkFromDoorToTable)
    delay(96)
    applymovement(LOCALID_DAISY, Common_Movement_WalkInPlaceUp)
    applymovement(LOCALID_SAM, Common_Movement_WalkInPlaceUp)
    turnobject(LOCALID_ORION, DIR_SOUTH)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    special(RemoveCameraObject)
    msgbox(format("Your friend already told us about what happened last night.\pI'm glad both of you are okay."))
    applymovement(LOCALID_ORIONS_MOM, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    turnobject(LOCALID_DAISY, DIR_EAST)
    turnobject(LOCALID_SAM, DIR_EAST)
    turnobject(LOCALID_ORION, DIR_EAST)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
    msgbox(format("As for the shady kids he told us about, we can't really make a decision on them since we don't know if they were intentionally causing trouble.\p"
                  "But, if you happen to get any more information on them, let both us and authorities know."))
    applymovement(LOCALID_SAM, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    turnobject(LOCALID_ORION, DIR_SOUTH)
    turnobject(LOCALID_DAISY, DIR_NORTH)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
    msgbox(format("Which leads to my next point.\p{PLAYER} and Orion, since this is your first time “owning” a Pokémon, we want you to pay a visit to the Professor's Lab in Animora City.\p"
                  "What happens next is entirely up to him."
                  "You can take Route 1, which is directly west of here, to get to Animora City."))
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    msgbox("Will do, Mr. Watson!")
    //applymovement(LOCALID_DAISY, Common_Movement_WalkInPlaceUp)
    //waitmovement(0)
    //msgbox(format("Before you head out of town, {PLAYER}, there's a box with your wardrobe in it in your room.\pYou can change clothes before you leave if you'd like."))
    applymovement(LOCALID_DAISY, Common_Movement_WalkInPlaceRight)
    applymovement(LOCALID_SAM, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("Mom: Alright, us two are gonna get going.\pThank you for having us over, Mrs. Fields."))
    applymovement(LOCALID_ORIONS_MOM, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("Anytime!"), MSGBOX_AUTOCLOSE)
    delay(20)
    applymovement(LOCALID_SAM, SamLeaveHouse)
    applymovement(LOCALID_DAISY, DaisyLeaveHouse)
    delay(96)
    playse(SE_EXIT)
    delay(48)
    playse(SE_EXIT)
    waitse
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("{PLAYER}, we have a problem!\nFollow me!"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerWalkToMons)
    applymovement(LOCALID_ORION, OrionWalkToMons)
    waitmovement(0)
    msgbox(format("When I went to go check on the Pokemon this morning, one of them was missing!\p"
                  "I looked all around the Town, but I couldn't find anything..."))
    closemessage
    delay(20)
    bufferleadmonspeciesname(STR_VAR_1)
    msgbox(format("Oh yeah! {PLAYER}, I want you to have this.\pYou can give it to {STR_VAR_1}!"))
    playfanfare(MUS_OBTAIN_ITEM)
	message(format("{PLAYER} received an Oran Berry!"))
    waitfanfare
    additem(ITEM_ORAN_BERRY)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("k see ya later"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    switch (var(VAR_STARTER_MON)) {
         case 0:
           removeobject(LOCALID_FS)
           applymovement(LOCALID_GS, MonEnterBall)
        case 1:
          removeobject(LOCALID_WS)
          applymovement(LOCALID_FS, MonEnterBall)
        case 2:
          removeobject(LOCALID_GS)
          applymovement(LOCALID_WS, MonEnterBall)
    }
    delay(20)
    applymovement(LOCALID_ORION, OrionWalkUpstairs)
    delay(48)
    removeobject(LOCALID_ORION)
    playse(SE_EXIT)
    waitse
    removeobject(LOCALID_DAISY)
    removeobject(LOCALID_SAM)
    removeobject(LOCALID_GS)
    removeobject(LOCALID_FS)
    removeobject(LOCALID_WS)
    setflag(FLAG_HIDE_CUTSCENE_NPCS_ORIONS_HOUSE)
    setvar(VAR_NEWGAME_START, 6)
    setvar(VAR_BLOCK_PROGRESSION_SOUTH, 1)
    clearflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
    clearflag(FLAG_HIDE_ORION_ORIONS_ROOM)
    setvar(VAR_BLOCK_PROGRESSION_WEST, 0)
    release
}

movement MonEnterBall {
    delay_16 * 2
    enter_pokeball
}

movement MoveCamToDoor {
    walk_diag_southwest * 2
    walk_left * 2
}

movement PlayerAppearAtDoor {
    set_visible
    delay_8
    delay_4
    walk_in_place_fast_right
}

movement OrionWalkToMons {
    walk_right * 2
    walk_up * 2
    walk_left * 5
    walk_in_place_right    
}

 movement PlayerWalkToMons {
    walk_right * 3
    walk_up * 2
    walk_left * 4
}

movement PlayerWalkToMon {
    walk_right
    walk_in_place_up
}

movement OrionWalkUpstairs {
    walk_left * 2
    walk_up
}

movement DaisyLeaveHouse {
    walk_down * 2
    walk_left * 2
    delay_16 * 2
    walk_left
    walk_in_place_down
    delay_16
    set_invisible
}

movement SamLeaveHouse {
    walk_down
    walk_left * 3
    walk_in_place_down
    delay_16
    set_invisible
}


movement MeetingPlayerWalkFromDoorToTable {
    walk_up * 3
    walk_right * 4
    walk_in_place_down
}

movement TownMeetingNoticePlayer {
    delay_8
    walk_in_place_left
    emote_exclamation_mark
    delay_16 * 3
}

movement MoveMomToTable {
    walk_up * 2
    walk_right * 6
    walk_down
    walk_in_place_left
}

movement OrionMomWalkToDoor {
    walk_left * 2
    walk_down * 2
    
}