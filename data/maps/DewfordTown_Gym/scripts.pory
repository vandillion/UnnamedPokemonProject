const LOCALID_GUIDE = 2

mapscripts DewfordTown_Gym_MapScripts {
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_IMPERIAL_GYM_STATE, 4 : ImperialGymHidePlayer
		VAR_IMPERIAL_GYM_STATE, 5 : ImperialGymHidePlayer
		VAR_IMPERIAL_GYM_STATE, 6 : ImperialGymHidePlayer
	]
	MAP_SCRIPT_ON_TRANSITION {
		if(!var(VAR_IMPERIAL_GYM_STATE)) {
			setobjectxyperm(LOCALID_GUIDE, 4, 13)
		}
		end
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_IMPERIAL_GYM_STATE, 0 {
			lock
			exclam(LOCALID_GUIDE)
			applymovement(LOCALID_GUIDE, moves(walk_down * 2))
			waitmovement(0)
			msgbox(format("Welcome young trainer, to the Imperial Island Gym!\p"
						  "Take this!"))
			giveitem(ITEM_FRESH_WATER)
			call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
			msgbox(format("I doubt you'll need it here though, because you'll be navigating though a water-submerged Gym!\p"
						  "Yep, that's right!\nBut before any of that, you'll need these!"))
			//giveitem(ITEM_IRON_BOOTS)
			call_if_eq(VAR_RESULT, FALSE, Common_EventScript_BagIsFull)
			msgbox(format("As I was just saying, majority of this gym is in fact underwater.\p"
						  "Using those fancy boots I just gave you, you will navigate through the gym to find and open 5 treasure chests.\p"
						  "But be careful! If you leave the area before finishing the challenge, any opened chests will close back and your progress will be lost.\p"
						  "Once you're ready, slip those Boots on and walk straight into the pool behind me!"), MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_GUIDE, moves(walk_up * 2
											   walk_in_place_fast_down))
			waitmovement(0)
			setvar(VAR_IMPERIAL_GYM_STATE, 1)
			release
			end
		}
		VAR_IMPERIAL_GYM_STATE, 4 {
			lock
			getobjectcurrentxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0, VAR_RESULT)
            switch(var(VAR_RESULT)) {
				case 11:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
													 jump_down))
				case 14:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
													 jump_up))
			}
			waitmovement(0)
			setvar(VAR_IMPERIAL_GYM_STATE, 3)
			release
			end
		}
		VAR_IMPERIAL_GYM_STATE, 5 {
			lock
			applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
													 jump_down))
			waitmovement(0)
			setvar(VAR_IMPERIAL_GYM_STATE, 1)
			release
			end
		}
		VAR_IMPERIAL_GYM_STATE, 6 {
			lock
			getobjectcurrentxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0, VAR_RESULT)
            switch(var(VAR_RESULT)) {
				case 11:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
													 jump_down))
				case 14:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
													 jump_up))
			}
			waitmovement(0)
			setvar(VAR_IMPERIAL_GYM_STATE, 2)
			release
			end
		}
	]
}

script(local) ImperialGymHidePlayer{
	hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN_GYM)
	end
}

script ImperialGymRedbeard {
	trainerbattle_single(TRAINER_BRAWLY_1,
	format("Arrrrgh… Another challenger approaches!\p"
	       "Avast! On the high seas or on land, ye are no match for the great Redbeard!\p"
	       "So give it up, matey!"), 
	format("Yaaaargh! Ye've sunken me ship!\nA fine battle that was, matey!"),
	ImperialGymRedbeard_Continue, NO_MUSIC)
	msgbox(format(""), MSGBOX_NPC)
	end
}

script ImperialGymRedbeard_Continue {
	setflag(FLAG_DEFEATED_IMPERIAL_GYM)
	playfanfare(MUS_OBTAIN_BADGE)
	message(format("{PLAYER} received the ??? Badge from Redbeard!"))
	waitfanfare
	setflag(FLAG_BADGE02_GET)
	setvar(VAR_0x8008, 2)
	call(Common_EventScript_SetGymTrainers)
	//msgbox("Take this too!")
	//giveitem(ITEM_TM81) //Aqua cutter
	//if(var(VAR_RESULT)) {
	//	setflag(FLAG_RECEIVED_TM_AQUA_CUTTER)
	//} else {
	//	call(Common_EventScript_BagIsFull)
	//}
	setvar(VAR_IMPERIAL_GYM_STATE, 3)
	setvar(VAR_KANE_STATE, 2)
	setflag(FLAG_HIDE_IMPERIAL_ISLAND_HOUSE1_KANE) // DO NOT DELETE UNTIL STORY UP TO GYM 2 IS COMPLETE!
	setflag(FLAG_HIDE_IMPERIAL_ISLAND_KANES_BOAT)  // ^
	end
}

script ImperialGymGuide {
	if(!defeated(TRAINER_BRAWLY_1)) {
		msgbox(format("This Gym's puzzle is primarily underwater.\p"
					  "Using those fancy boots I gave you, you will navigate through the Gym to find and open 5 treasure chests.\p"
					  "But be careful! If you happen to leave the area before finishing the challenge, any opened chests will close and your progress will be lost.\p"
					  "Alright, Go get 'em!\nAnd try not to drown, haha!"), MSGBOX_NPC)
	} else {
		msgbox(format("Another solid victory for a Champion-\nbound Trainer! Congrats, {PLAYER}!\p"
					  "Now let me ask you! Did that challenge feel…{PAUSE 40} nostalgic at all?"), MSGBOX_NPC)
	}
	end
}

script ImperialGymStatue {
	msgbox("Imperial Island Pokémon Gym\nLeader: Redbeard\p"
		   "The ????????????!", MSGBOX_SIGN)
	end
}

script ImperialGym_EnterPool {
	lock
	delay(2)
	playse(SE_FU_ZAKU)
	getobjectfacingdirection(OBJ_EVENT_ID_PLAYER, VAR_RESULT)
	switch(var(VAR_RESULT)) {
		case DIR_NORTH:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_up * 4))
		case DIR_SOUTH:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_down * 4))
		case DIR_EAST:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_right * 4))
		case DIR_WEST:
			applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_left * 4))
	}
	waitse
	hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_DEWFORD_TOWN_GYM)
	playse(SE_M_DIVE)
	fadescreen(FADE_TO_BLACK)
	waitse
	getobjectcurrentxy(OBJ_EVENT_ID_PLAYER, VAR_TEMP_0, VAR_TEMP_1)
	if(var(VAR_TEMP_1) <= 7) {
		warpnofade(MAP_DEWFORD_TOWN_GYM, 4, 11)
		setvar(VAR_IMPERIAL_GYM_STATE, 4)
	} elif(var(VAR_TEMP_0) > 13) {
		warpnofade(MAP_IMPERIAL_ISLAND_GYM_B1F, 22, 3)
	} else {
		warpnofade(MAP_IMPERIAL_ISLAND_GYM_B1F, 22, 43)
	}
	waitstate
	release
	end
}