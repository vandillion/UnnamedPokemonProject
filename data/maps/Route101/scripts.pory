raw `
.set LOCALID_HARLEY, 2
`

mapscripts Route101_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call_if_eq(VAR_ROUTE1_STATE, 0, Route1UpdateHarley)
	}
}

script Route1UpdateHarley {
	setobjectmovementtype(LOCALID_HARLEY, MOVEMENT_TYPE_INVISIBLE)
}

//====================General Scripts====================

script Route1Sign1 {
	msgbox("Route 1\p{UP_ARROW} Animora City\n{RIGHT_ARROW} Woodridge Town", MSGBOX_SIGN)
	end
}

script Route1Sign2 {
	msgbox("Route 1\p{LEFT_ARROW} Animora City\n{RIGHT_ARROW} Woodridge Town", MSGBOX_SIGN)
	end
}

//====================Harley====================

script HarleyBattleTrigger0 {
	setvar(VAR_TEMP_0, 0)
	goto(HarleyBattle)
}

script HarleyBattleTrigger1 {
	setvar(VAR_TEMP_0, 1)
	goto(HarleyBattle)
}

script HarleyBattleTrigger2 {
	setvar(VAR_TEMP_0, 2)
	goto(HarleyBattle)
}

script HarleyBattle {
	lock
	setobjectxy(LOCALID_HARLEY, 6, 14)
	playbgm(MUS_NONE, TRUE)
	msgbox("???: YOU!")
	closemessage
	playbgm(MUS_RG_ENCOUNTER_GYM_LEADER, FALSE)
	applymovement(OBJ_EVENT_ID_PLAYER, WalkInPlaceDownExclam)
	delay(16)
	playse(SE_PIN)
	delay(20)
	switch (var(VAR_TEMP_0)) {
        case 0:
			applymovement(LOCALID_HARLEY, HarleyWalkTowardsPlayer0)
        case 1:
			applymovement(LOCALID_HARLEY, HarleyWalkTowardsPlayer1)
        case 2:
			applymovement(LOCALID_HARLEY, HarleyWalkTowardsPlayer2)
    }
	waitmovement(0)
	delay(30)
	msgbox("Battle me!\nNow!")
	goto_if_set(FLAG_LOST_FIRST_BATTLE, ContinueLostFirstBattleHarley)
	switch (var(VAR_STARTER_MON)) {
        case 0:
			trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TREECKO, HarleyDefeat)
        case 1:
			trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_TORCHIC, HarleyDefeat)
        case 2:
			trainerbattle_no_intro(TRAINER_MAY_ROUTE_103_MUDKIP, HarleyDefeat)
    }
	goto(PostBattleHarley1)
}

script ContinueLostFirstBattleHarley {
	switch (var(VAR_STARTER_MON)) {
        case 0:
			trainerbattle_no_intro(TRAINER_MAY_ROUTE_110_TREECKO, HarleyDefeat)
        case 1:
			trainerbattle_no_intro(TRAINER_MAY_ROUTE_110_TORCHIC, HarleyDefeat)
        case 2:
			trainerbattle_no_intro(TRAINER_MAY_ROUTE_110_MUDKIP, HarleyDefeat)
	}
	goto(PostBattleHarley1)
}

text HarleyDefeat {
	"..."
}

script PostBattleHarley1 {
	msgbox("...\n...\pWe'll meet again...")
	closemessage
	playse(SE_FLEE)
	switch (var(VAR_TEMP_0)) {
        case 0:
			applymovement(LOCALID_HARLEY, Route1HarleyLeave0)
			delay(8)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
			delay(16)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
        default:
			applymovement(LOCALID_HARLEY, Route1HarleyLeave1)
			delay(8)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
			delay(16)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    }
	waitmovement(0)
	fadedefaultbgm
	removeobject(LOCALID_HARLEY)
	setflag(FLAG_HIDE_HARLEY_ROUTE_1)
	setflag(FLAG_HIDE_AIDE_ANIMORA_CITY)
	setvar(VAR_ROUTE1_STATE, 1)
	release
}

movement Route1HarleyLeave0 {
	walk_fast_right
	walk_fast_up  * 7
}

movement Route1HarleyLeave1 {
	walk_fast_left
	walk_fast_up  * 7
}

movement WalkInPlaceDownExclam {
	walk_in_place_down
	emote_exclamation_mark
}

movement HarleyWalkTowardsPlayer0 {
	set_visible
	walk_fast_up  * 3
	walk_fast_right
	walk_fast_up * 2
}

movement HarleyWalkTowardsPlayer1 {
	set_visible
	walk_fast_up  * 2
	walk_fast_right
	walk_fast_up
	walk_fast_right
	walk_fast_up * 2
}

movement HarleyWalkTowardsPlayer2 {
	set_visible
	walk_fast_up * 2
	walk_fast_right * 2
	walk_fast_up
	walk_fast_right
	walk_fast_up * 2
}

