const LOCALID_CLASSMATE = 1

mapscripts Route129_MapScripts {}

script CentraSWClassmateTrigger {
	goto_if_not_defeated(TRAINER_ROXANNE_1, Common_EventScript_BlockProgression)
	lock
	getobjectxy(OBJ_EVENT_ID_PLAYER, CURRENT_POSITION, VAR_TEMP_0, VAR_TEMP_1)
	switch(var(VAR_TEMP_0)) {
		case 16:
			setobjectxy(LOCALID_CLASSMATE, 24, 57)
		case 17:
			setobjectxy(LOCALID_CLASSMATE, 25, 57)
		case 18:
			setobjectxy(LOCALID_CLASSMATE, 26, 57)
	}
	applymovement(LOCALID_CLASSMATE, moves(walk_left * 3
											emote_exclamation_mark
											delay_16 * 2
											walk_left * 5
											walk_down))
	delay(48)
	playse(SE_PIN)
	delay(16)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	delay(72)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	waitmovement(0)
	msgbox(format("Oh hey, {PLAYER}!\nHow've you been?\p"
					"It's me, Nolan! From your 2nd homeroom at the Pokémon Academy!\p"
					"Looks like you're headed to Route 3, huh? Can I ask of you a huge favor?\p"
					"I have a family member who lives on the edge of Route 3 that I need to deliver this Letter to.\p"
					"Since you're already on your way there, can I ask you to deliver the Letter for me?"))
	giveitem(ITEM_LETTER)
	msgbox(format("Thanks a million, {PLAYER}!\nYou're the best, really!\p"
					"Also, good luck on your Pokémon journey!"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_CLASSMATE, moves(walk_up
											walk_left * 10))
	waitmovement(0)
	removeobject(LOCALID_CLASSMATE)
	setflag(FLAG_HIDE_CENTRA_SW_CLASSMATE)
	setvar(VAR_CENTRA_SW_STATE, 1)
	release
	end
}