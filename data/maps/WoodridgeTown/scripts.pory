const LOCALID_ORION = 1
const LOCALID_GS = 2
const LOCALID_FS = 3
const LOCALID_WS = 4

mapscripts WoodridgeTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_WOODRIDGE_TOWN)
        setrespawn(HEAL_LOCATION_WOODRIDGE_TOWN)
		if(var(VAR_NEWGAME_START) == 5) {
			setobjectxyperm(LOCALID_ORION, 16, 31)
		} elif(var(VAR_NEWGAME_START) == 6) {
			setobjectxyperm(LOCALID_ORION, 1, 11)
			setobjectmovementtype(LOCALID_ORION, MOVEMENT_TYPE_INVISIBLE)
		}
		end
	}
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_NEWGAME_START, 5 {
			playbgm(MUS_NONE, TRUE)
			hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_WOODRIDGE_TOWN)
			end
		}
    ]
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_NEWGAME_START, 2 { // Orion meets player for the first time and introduces himself, and asks the player out on a walk later that day
    		lock
			applymovement(LOCALID_ORION, moves(walk_up * 4
											   walk_left
											   walk_up
											   face_player))
			waitmovement(0)
			applymovement(LOCALID_ORION, Common_Movement_ExclamationMarkDelay)
			playse(SE_PIN)
			delay(8)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
			waitmovement(LOCALID_ORION)
			applymovement(LOCALID_ORION, moves(walk_right * 3
											   walk_in_place_up))
			delay(32)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
			waitmovement(LOCALID_ORION)
			playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
			msgbox(format("Who are you?\p"
						  "… … …\n… … …\p"
						  "{PLAYER}, huh?\nNice to meet you! My name's Orion.\p"
						  "Your family just moved to this region, correct?\p"
						  "If you're not yet familiar with this area, how about a take you on a walk?\p"
						  "There's a forest southeast from here that I like to retreat to, and I think you'll lke it.\p"
						  "Great! See ya later tonight!\nLet your parents know first!"), MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_ORION, moves(walk_up))
			applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction
													 walk_left
													 unlock_facing_direction
													 face_right))
			waitmovement(0)
			opendoor(18, 7)
			waitdooranim
			applymovement(LOCALID_ORION, moves(walk_up))
			waitmovement(0)
			removeobject(LOCALID_ORION)
			fadedefaultbgm
			closedoor(18, 7)
			waitdooranim
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
			setflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
			clearflag(FLAG_HIDE_ORION_ORIONS_ROOM)
			setvar(VAR_NEWGAME_START, 3)
			release
			end
		}
		 VAR_NEWGAME_START, 5 { // Cutscene after the Vast Woods Trail battle
			lock
			special(SpawnCameraObject)
			setobjectxy(OBJ_EVENT_ID_PLAYER, 17, 31)
			applymovement(LOCALID_GS, moves(delay_8
											walk_fast_up * 5
											walk_fast_left
											walk_fast_up * 4
											walk_up * 5))
			applymovement(LOCALID_FS, moves(delay_8
											walk_fast_up * 5
											walk_fast_left
											walk_fast_up * 4
											walk_up * 5))
			applymovement(LOCALID_WS, moves(delay_16
											walk_fast_up * 5
											walk_fast_left
											walk_fast_up * 4
											walk_up * 4))
			applymovement(OBJ_EVENT_ID_CAMERA, moves(delay_16 * 3
													 delay_8
													 walk_fast_up * 3
													 walk_up * 6))
			applymovement(OBJ_EVENT_ID_PLAYER, moves(set_visible
													 player_run_up * 5
													 player_run_left
													 player_run_up * 4
													 walk_up * 6
													 delay_16 * 2
													 walk_in_place_fast_down))
			applymovement(LOCALID_ORION, moves(set_visible
											   walk_fast_up * 5
											   walk_fast_left
											   walk_fast_up * 4
											   walk_up * 6
											   delay_16 * 2
											   walk_in_place_fast_down))
			waitmovement(0)
			special(RemoveCameraObject)
			delay(20)
			msgbox(format("Okay, it looks like we weren't followed."))
			applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
			waitmovement(0)
			msgbox(format("{PLAYER}, It's getting really late.\nYou should head home.\p"
						  "Tell your parents to meet at my house in the morning.\p"
						  "In the mean time, the Pokemon will reside by the pond for the night.\pLet's just hope those turds don't plan on entering our town."), MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_ORION, moves(walk_in_place_down
											   delay_16 * 2
											   walk_in_place_up))
			waitmovement(0)
			applymovement(LOCALID_ORION, moves(walk_up
											   walk_right
											   walk_up * 4
											   walk_left
											   walk_up))
			applymovement(LOCALID_GS, moves(walk_up * 2
											walk_right
											walk_up * 4
											walk_left
											walk_up))
			applymovement(LOCALID_FS, moves(walk_left
											walk_up * 2
											walk_right
											walk_up * 4
											walk_left
											walk_up))
			applymovement(LOCALID_WS, moves(delay_16
											walk_up * 3
											walk_right
											walk_up * 4
											walk_left
											walk_up))
			delay(16)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
			waitmovement(LOCALID_WS)
			setflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
			setflag(FLAG_HIDE_POKEMON_WOODRIDGE_TOWN)
			clearflag(FLAG_HIDE_CUTSCENE_NPCS_ORIONS_HOUSE)
			delay(30)
			warpsilent(MAP_WOODRIDGE_TOWN_ORIONS_HOUSE1F, 6, 5)
			waitstate
			release
			end
		}
    ]
}

// NPCs

// Signs, etc

script WoodridgeTownSign {
	msgbox("Woodridge Town", MSGBOX_SIGN)
	end
}

script PlayersHouseSign {
	msgbox("The ??????? Residence", MSGBOX_SIGN)
	end
}

script OrionsHouseSign {
	msgbox("The Fields Residence", MSGBOX_SIGN)
	end
}

script WoodridgeTown3rdHouseScript {
	if (var(VAR_ANIMORA_CITY_STATE) >= 2 || var(VAR_NEWGAME_START) < 6) {
		msgbox(format("This door is locked."), MSGBOX_SIGN)
	} else {
		msgbox(format("The door feels like it's barricaded shut.\pYou can hear faint snoring coming from inside."), MSGBOX_SIGN)
	}
	end
}

// BGs

script WoodridgeFirstBattle {
	lock
	addvar(VAR_NEWGAME_START, 1)
	setobjectxy(LOCALID_ORION, 9, 13)
	msgbox("Wait up, {PLAYER}!", MSGBOX_AUTOCLOSE)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	switch (var(VAR_TEMP_1)) {
        case 0:
			applymovement(LOCALID_ORION, moves(set_visible
											   walk_left * 8
											   face_up))
			delay(112)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
        case 1:
			applymovement(LOCALID_ORION, moves(set_visible
											   walk_left * 4
											   walk_down
											   walk_left * 4
											   face_up))
			delay(112)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
        case 2:
			applymovement(LOCALID_ORION, moves(set_visible
											   walk_left * 8
											   face_down))
			delay(112)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
        case 3:
			applymovement(LOCALID_ORION, moves(set_visible
											   walk_left * 5
											   walk_down
											   walk_left * 3
											   face_down))
			delay(128)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    }
	waitmovement(LOCALID_ORION)
	playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
	msgbox(format("Since we're about to start our adventures as trainers, it's right we have a proper battle!\p"
				  "Alright!\nNo holding back, {PLAYER}!"))
	setrespawn(HEAL_LOCATION_WOODRIDGE_TOWN_PLAYERS_HOUSE_2F)
	if (!flag(FLAG_LOST_FIRST_BATTLE)) {
		switch (var(VAR_STARTER_MON)) {
        	case 0:
				trainerbattle_no_intro_no_whiteout(TRAINER_BRENDAN_ROUTE_103_TREECKO, OrionDefeat)
        	case 1:
				trainerbattle_no_intro_no_whiteout(TRAINER_BRENDAN_ROUTE_103_TORCHIC, OrionDefeat)
        	case 2:
				trainerbattle_no_intro_no_whiteout(TRAINER_BRENDAN_ROUTE_103_MUDKIP, OrionDefeat)
	    }
    } else {
		switch (var(VAR_STARTER_MON)) {
        	case 0:
				trainerbattle_no_intro_no_whiteout(TRAINER_BRENDAN_ROUTE_110_TREECKO, OrionDefeat)
        	case 1:
				trainerbattle_no_intro_no_whiteout(TRAINER_BRENDAN_ROUTE_110_TORCHIC, OrionDefeat)
        	case 2:
				trainerbattle_no_intro_no_whiteout(TRAINER_BRENDAN_ROUTE_110_MUDKIP, OrionDefeat)
    	}
	}
	if(defeated(TRAINER_BRENDAN_ROUTE_103_TREECKO)
    || defeated(TRAINER_BRENDAN_ROUTE_103_TORCHIC)
    || defeated(TRAINER_BRENDAN_ROUTE_103_MUDKIP)
    || defeated(TRAINER_BRENDAN_ROUTE_110_TREECKO)
    || defeated(TRAINER_BRENDAN_ROUTE_110_TORCHIC)
    || defeated(TRAINER_BRENDAN_ROUTE_110_MUDKIP)) {
			msgbox(format("That battle could've gone either way.\nIt was that close!\p
			               Alright {PLAYER}, I'm gonna start heading over to Animora City.\p
						   See you later!"), MSGBOX_AUTOCLOSE)
	} else {
			msgbox(format("Alright! I won! But that battle could've gone either way. It was that close!\p
			        	   Alright {PLAYER}, I'm gonna start heading over to Animora City.\p
					       See you later!"), MSGBOX_AUTOCLOSE)

	}
	applymovement(LOCALID_ORION, moves(walk_left * 8))
	delay(36)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
	waitmovement(0)
	removeobject(LOCALID_ORION)
	fadedefaultbgm
	setflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
	setflag(FLAG_HIDE_ORION_ORIONS_ROOM)
	clearflag(FLAG_HIDE_MAP_NAME_POPUP)
	clearflag(FLAG_UNUSED_0x068)
	setvar(VAR_BLOCK_PROGRESSION_WEST, 2)
	special(HealPlayerParty)
	release
}

text OrionDefeat {
	format("What a great battle!\nWe're going to be strong trainers someday, I know it!")
}

script WoodridgeBattleTrigger0 {
	setvar(VAR_TEMP_1, 0)
	goto(WoodridgeFirstBattle)
}

script WoodridgeBattleTrigger1 {
	setvar(VAR_TEMP_1, 1)
	goto(WoodridgeFirstBattle)
}


script WoodridgeBattleTrigger2 {
	setvar(VAR_TEMP_1, 2)
	goto(WoodridgeFirstBattle)
}


script WoodridgeBattleTrigger3 {
	setvar(VAR_TEMP_1, 3)
	goto(WoodridgeFirstBattle)
}