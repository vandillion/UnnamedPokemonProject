raw `
.set LOCALID_ORION, 1
.set LOCALID_GS, 2
.set LOCALID_FS, 3
.set LOCALID_WS, 4
`

mapscripts WoodridgeTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call_if_eq(VAR_NEWGAME_START, 5, UpdateObjects)
		call_if_eq(VAR_NEWGAME_START, 6, UpdateOrionForBattle)
	}
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		 VAR_NEWGAME_START, 5 : WoodridgeTownHidePlayer
    ]
	MAP_SCRIPT_ON_FRAME_TABLE [
		 VAR_NEWGAME_START, 2 : OrionIntro
		 VAR_NEWGAME_START, 5 : CutsceneAfterFirstBattle
    ]
}

script WoodridgeTownHidePlayer {
	hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_WOODRIDGE_TOWN)
}

script UpdateOrionForBattle {
	setobjectxyperm(LOCALID_ORION, 1, 12)
	setobjectmovementtype(LOCALID_ORION, MOVEMENT_TYPE_INVISIBLE)
	}

script UpdateObjects {
	setobjectxyperm(LOCALID_ORION, 13, 26)
	setobjectxyperm(LOCALID_GS, 13, 27)
	setobjectxyperm(LOCALID_FS, 14, 27)
	setobjectxyperm(LOCALID_WS, 15, 27)
	switch (var(VAR_STARTER_MON)) {
        case 0:
			setobjectmovementtype(LOCALID_WS, MOVEMENT_TYPE_INVISIBLE)
        case 1:
            setobjectmovementtype(LOCALID_GS, MOVEMENT_TYPE_INVISIBLE)
        case 2:
			setobjectmovementtype(LOCALID_FS, MOVEMENT_TYPE_INVISIBLE)
    }
	}

//General town scripts

script WoodridgeTownSign {
	msgbox("Woodridge Town", MSGBOX_SIGN)
	end
}

script PlayersHouseSign {
	msgbox("The Oak-Watson Residence", MSGBOX_SIGN)
	end
}

script OrionsHouseSign {
	msgbox("The Fields Residence", MSGBOX_SIGN)
	end
}

//Triggers

script WoodridgeBattleTrigger0 {
	setvar(VAR_TEMP_1, 0)
	goto(WoodridgeFirstBattle)
}

script WoodridgeBattleTrigger1 {
	setvar(VAR_TEMP_1, 1)
	goto(WoodridgeFirstBattle)
}


script WoodridgeBattleTrigger2 {
	setvar(VAR_TEMP_1, 2)
	goto(WoodridgeFirstBattle)
}


script WoodridgeBattleTrigger3 {
	setvar(VAR_TEMP_1, 3)
	goto(WoodridgeFirstBattle)
}

script WoodridgeFirstBattle {
	lock
	setobjectxy(LOCALID_ORION, 10, 14)
	msgbox("Wait up, {PLAYER}!")
	closemessage
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	switch (var(VAR_TEMP_1)) {
        case 0:
			applymovement(LOCALID_ORION, OrionBattleMovement0)
			delay(112)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
        case 1:
			applymovement(LOCALID_ORION, OrionBattleMovement1)
			delay(112)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
        case 2:
			applymovement(LOCALID_ORION, OrionBattleMovement2)
        case 3:
			applymovement(LOCALID_ORION, OrionBattleMovement3)
			delay(112)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
    }
	waitmovement(LOCALID_ORION)
	msgbox(format("Since we're about to start our adventures as trainers, it's right we have a proper battle!\pNo holding back, {PLAYER}!"))
	goto_if_set(FLAG_LOST_FIRST_BATTLE, ContinueLostFirstBattle)
	switch (var(VAR_STARTER_MON)) {
        case 0:
			trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TREECKO, OrionDefeat)
        case 1:
			trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_TORCHIC, OrionDefeat)
        case 2:
			trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_103_MUDKIP, OrionDefeat)
    }
	goto(PostFirstBattle)
}

script ContinueLostFirstBattle {
	switch (var(VAR_STARTER_MON)) {
        case 0:
			trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_110_TREECKO, OrionDefeat)
        case 1:
			trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_110_TORCHIC, OrionDefeat)
        case 2:
			trainerbattle_no_intro(TRAINER_BRENDAN_ROUTE_110_MUDKIP, OrionDefeat)
	}
	goto(PostFirstBattle)
}

script PostFirstBattle {
	msgbox("Alright, I'm headin out\nSee ya later, {PLAYER}!")
	closemessage
	switch (var(VAR_TEMP_1)) {
        case 2:
			applymovement(LOCALID_ORION, OrionLeave2)
			delay(16)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
			delay(32)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
        default:
			applymovement(LOCALID_ORION, OrionLeave)
			delay(16)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
			
    }
	waitmovement(0)
	removeobject(LOCALID_ORION)
	setflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
	setflag(FLAG_HIDE_ORION_ORIONS_ROOM)
	clearflag(FLAG_HIDE_MAP_NAME_POPUP)
	setvar(VAR_NEWGAME_START, 7)
	special(HealPlayerParty)
	release
}

movement OrionLeave {
	walk_left * 6
	walk_down
	walk_left
	walk_down
	walk_left
	}

movement OrionLeave2 {
	walk_up
	walk_left * 7
	walk_down
	walk_left
	walk_down
	walk_left
}

text OrionDefeat {
	format("What a great battle!\nWe're going to be strong trainers someday, I know it!")
}

movement OrionBattleMovement0 {
	set_visible
	walk_left * 4
	walk_up
	walk_left * 4
	walk_in_place_up
}

movement OrionBattleMovement1 {
	set_visible
	walk_left * 8
	walk_in_place_up
}

movement OrionBattleMovement2 {
	set_visible
	walk_left * 7
}

movement OrionBattleMovement3 {
	set_visible
	walk_left * 8
	walk_in_place_down
}

script BlockProgressionW {
	lock
	call_if_lt(VAR_NEWGAME_START, 7, pensiw)
	release
}

script pensiw {
	msgbox(BlockProg)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, BlockProgW)
	waitmovement(0)
}

script BlockProgressionS {
	lock
	call_if_lt(VAR_NEWGAME_START, 4, pensis)
	release
}

script pensis {
	msgbox(BlockProg)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, BlockProgS)
	waitmovement(0)
}

text BlockProg {"You can't leave yet!"}
movement BlockProgN {walk_down}
movement BlockProgE {walk_left}
movement BlockProgS {walk_up}
movement BlockProgW {walk_right}

//Story scripts

script OrionIntro {
    lock
	applymovement(LOCALID_ORION, OrionWalkHomeFromWoods)
	waitmovement(0)
	applymovement(LOCALID_ORION, Common_Movement_ExclamationMarkDelay)
	playse(SE_PIN)
	delay(15)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
	waitmovement(LOCALID_ORION)
	applymovement(LOCALID_ORION, ContinueWalkFromWoods)
	delay(48)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	waitmovement(LOCALID_ORION)
	msgbox(format("Who are you?\p...\n...\pSo you're {PLAYER}\nNice to meet you!\lMay name's Orion.\pI heard you're new around here.\nMeet me near the south of town to go on a walk.\pLet your parents know first!"))
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, PlayerMoveOutWay)
	delay(16)
	applymovement(LOCALID_ORION, MoveUp)
	waitmovement(0)
	opendoor(19, 11)
	waitdooranim
	applymovement(LOCALID_ORION, MoveUp)
	waitmovement(0)
	removeobject(LOCALID_ORION)
	closedoor(19, 11)
	waitdooranim
	setflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
	clearflag(FLAG_HIDE_ORION_ORIONS_ROOM)
	setvar(VAR_NEWGAME_START, 3)
	release
}


movement OrionWalkHomeFromWoods {
	walk_up * 5
	face_right}


movement ContinueWalkFromWoods {
	walk_right * 5
	walk_in_place_up	
}

movement PlayerMoveOutWay {
	lock_facing_direction
	walk_left
	unlock_facing_direction
	face_right
}
	

movement MoveUp{
    walk_up
}


//---------------------------------------------------------------------------------

script CutsceneAfterFirstBattle {
	lock
	special(SpawnCameraObject)
	setobjectxy(OBJ_EVENT_ID_PLAYER, 14, 26)
	applymovement(OBJ_EVENT_ID_PLAYER, RunHome0)
	applymovement(LOCALID_ORION, RunHome1)
	applymovement(LOCALID_GS, RunHome1)
	applymovement(LOCALID_FS, RunHome1)
	applymovement(LOCALID_WS, RunHome2)
	applymovement(OBJ_EVENT_ID_CAMERA, RunHome3)
	waitmovement(0)
	delay(16)
	special(RemoveCameraObject)
	applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	delay(30)
	msgbox(format("Okay, it looks like we weren't followed."))
	closemessage
	applymovement(LOCALID_ORION, LookForMon)
	waitmovement(0)
	applymovement(LOCALID_ORION, Common_Movement_ExclamationMarkDelay)
	waitmovement(0)
	msgbox(format("What?\nOne of the Pokemon is missing!\pOh, well!\nWe already have 2 of them, and it's getting really late."))
	applymovement(LOCALID_ORION, Common_Movement_WalkInPlaceRight)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	msgbox(format("{PLAYER}, you should head home.\nTell your parents to meet at my house in the morning.\p"
				  "In the mean time, these two will reside at the pond for the night.\pLet's just hope those turds don't plan on entering our town."))
	closemessage
	applymovement(LOCALID_ORION, WalkPrepare)
	waitmovement(0)
	applymovement(LOCALID_ORION, WalkMons0)
	applymovement(LOCALID_GS, WalkMons1)
	applymovement(LOCALID_FS, WalkMons2)
	applymovement(LOCALID_WS, WalkMons3)
	waitmovement(0)
	setflag(FLAG_HIDE_ORION_WOODRIDGE_TOWN)
	setflag(FLAG_HIDE_POKEMON_WOODRIDGE_TOWN)
	clearflag(FLAG_HIDE_CUTSCENE_NPCS_ORIONS_HOUSE)
	delay(30)
	warpsilent(MAP_WOODRIDGE_TOWN_ORIONS_HOUSE1F, 6, 5)
	waitstate
	release
}

movement RunHome3 {
	delay_16 * 4
	walk_up
}

movement LookForMon {
	walk_in_place_fast_left
	delay_16 * 2
	walk_in_place_fast_right
	delay_16 * 2
	walk_in_place_fast_down
	delay_16
}

movement RunHome0 {
	set_visible
	player_run_up * 4
	walk_up * 3
}

movement RunHome1 {
	walk_fast_up * 4
	walk_up * 3
}

movement RunHome2 {
	walk_fast_left
	walk_fast_up * 3
	walk_up * 3
}

movement WalkPrepare {
	walk_in_place_down
	delay_16 * 2
	walk_in_place_up
}

movement WalkMons0 {
	walk_up * 6
}

movement WalkMons1 {
	walk_up * 7
}

movement WalkMons2 {
	walk_left
	walk_up * 7
}

movement WalkMons3 {
	walk_up
	walk_left
	walk_up * 7
}