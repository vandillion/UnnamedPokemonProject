raw `
.set LOCALID_ORION, 1
.set LOCALID_GS, 2
.set LOCALID_FS, 3
.set LOCALID_WS, 4
`

mapscripts WoodridgeTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call_if_eq(VAR_NEWGAME_START, 5, UpdateObjects)
	}
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		 VAR_NEWGAME_START, 5 : UpdatePlayer
    ]
	MAP_SCRIPT_ON_FRAME_TABLE [
		 VAR_NEWGAME_START, 2 : OrionIntro
		 VAR_NEWGAME_START, 5 : CutsceneAfterFirstBattle
    ]
}

script UpdatePlayer {
	hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_WOODRIDGE_TOWN)
}

script UpdateObjects {
	setobjectxyperm(LOCALID_ORION, 13, 26)
	setobjectxyperm(LOCALID_GS, 13, 27)
	setobjectxyperm(LOCALID_FS, 14, 27)
	setobjectxyperm(LOCALID_WS, 15, 27)
	switch (var(VAR_STARTER_MON)) {
        case 0:
			setobjectmovementtype(LOCALID_WS, MOVEMENT_TYPE_INVISIBLE)
        case 1:
            setobjectmovementtype(LOCALID_GS, MOVEMENT_TYPE_INVISIBLE)
        case 2:
			setobjectmovementtype(LOCALID_FS, MOVEMENT_TYPE_INVISIBLE)
    }
	}

//General town scripts

script WoodridgeTownSign {
	lock
	msgbox("Woodridge Town", MSGBOX_SIGN)
	release
	end
}

script PlayersHouseSign {
	lock
	msgbox("The Oak-Watson Residence", MSGBOX_SIGN)
	release
	end
}

script OrionsHouseSign {
	lock
	msgbox("The Fields Residence", MSGBOX_SIGN)
	release
	end
}

//Story scripts

script OrionIntro {
    lock
	applymovement(LOCALID_ORION, OrionWalkHomeFromWoods)
	waitmovement(0)
	applymovement(LOCALID_ORION, Common_Movement_ExclamationMarkDelay)
	playse(SE_PIN)
	delay(15)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
	waitmovement(LOCALID_ORION)
	applymovement(LOCALID_ORION, ContinueWalkFromWoods)
	delay(48)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	waitmovement(LOCALID_ORION)
	msgbox(format("Who are you?\p...\n...\pSo you're {PLAYER}\nNice to meet you!\lMay name's Orion.\pI heard you're new around here.\nMeet me near the south of town to go on a walk.\pLet your parents know first!"))
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, PlayerMoveOutWay)
	delay(16)
	applymovement(LOCALID_ORION, MoveUp)
	waitmovement(0)
	opendoor(19, 11)
	waitdooranim
	applymovement(LOCALID_ORION, MoveUp)
	waitmovement(0)
	removeobject(LOCALID_ORION)
	closedoor(19, 11)
	waitdooranim
	setflag(FLAG_UNUSED_0x020)
	clearflag(FLAG_UNUSED_0x021)
	setvar(VAR_NEWGAME_START, 3)
	release
}


movement OrionWalkHomeFromWoods {
	walk_up * 5
	face_right}


movement ContinueWalkFromWoods {
	walk_right * 5
	walk_in_place_up	
}

movement PlayerMoveOutWay {
	lock_facing_direction
	walk_left
	unlock_facing_direction
	face_right
}
	

movement MoveUp{
    walk_up
}


//---------------------------------------------------------------------------------

script CutsceneAfterFirstBattle {
	lock
	special(SpawnCameraObject)
	setobjectxy(OBJ_EVENT_ID_PLAYER, 14, 26)
	applymovement(OBJ_EVENT_ID_PLAYER, RunHome0)
	applymovement(LOCALID_ORION, RunHome1)
	applymovement(LOCALID_GS, RunHome1)
	applymovement(LOCALID_FS, RunHome1)
	applymovement(LOCALID_WS, RunHome2)
	applymovement(OBJ_EVENT_ID_CAMERA, RunHome3)
	waitmovement(0)
	delay(16)
	special(RemoveCameraObject)
	applymovement(LOCALID_ORION, mWIPD)
	applymovement(OBJ_EVENT_ID_PLAYER, mWIPD)
	waitmovement(0)
	delay(30)
	msgbox(format("Okay, it looks like we weren't followed."))
	closemessage
	applymovement(LOCALID_ORION, LookForMon)
	waitmovement(0)
	applymovement(LOCALID_ORION, Common_Movement_ExclamationMarkDelay)
	waitmovement(0)
	msgbox(format("What?\nOne of the Pokemon is missing!\pOh, well!\nWe already have 2 of them, and it's getting really late."))
	applymovement(LOCALID_ORION, mWIPR)
	applymovement(OBJ_EVENT_ID_PLAYER, mWIPL)
	waitmovement(0)
	msgbox(format("{PLAYER}, you should head home.\nTell your parents to meet at my house in the morning.\p"
				  "In the mean time, these two will reside at the pond for the night.\pLet's just hope those turds don't plan on entering our town."))
	closemessage
	applymovement(LOCALID_ORION, WalkPrepare)
	waitmovement(0)
	applymovement(LOCALID_ORION, WalkMons0)
	applymovement(LOCALID_GS, WalkMons1)
	applymovement(LOCALID_FS, WalkMons2)
	applymovement(LOCALID_WS, WalkMons3)
	waitmovement(0)
	delay(30)
	setflag(FLAG_UNUSED_0x020)
	setflag(FLAG_UNUSED_0x022)
	setvar(VAR_NEWGAME_START, 6)
	warpsilent(MAP_WOODRIDGE_TOWN_PLAYERS_HOUSE2F, 1, 4)
	waitstate
	release
}

movement RunHome3 {
	delay_16 * 4
	walk_up
}

movement LookForMon {
	walk_in_place_fast_left
	delay_16 * 2
	walk_in_place_fast_right
	delay_16 * 2
	walk_in_place_fast_down
	delay_16
}

movement RunHome0 {
	set_visible
	player_run_up * 4
	walk_up * 3
}

movement RunHome1 {
	walk_fast_up * 4
	walk_up * 3
}

movement RunHome2 {
	walk_fast_left
	walk_fast_up * 3
	walk_up * 3
}

movement WalkPrepare {
	walk_in_place_down
	delay_16 * 2
	walk_in_place_up
}

movement WalkMons0 {
	walk_up * 6
}

movement WalkMons1 {
	walk_up * 7
}

movement WalkMons2 {
	walk_left
	walk_up * 7
}

movement WalkMons3 {
	walk_up
	walk_left
	walk_up * 7
}

movement mWIPD {
	walk_in_place_down
}